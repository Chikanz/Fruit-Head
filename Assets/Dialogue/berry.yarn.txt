title: BerryIslandStart
tags: 
colorID: 5
position: -717,-916
---
AVERY: Berry Island! We made it! 
AVERY: I'd forgotten how much I hate ships.
CHARLIE: I thought you said you'd never left Blossom Island before.
AVERY: ...I haven't.
CHARLIE: But you just said you hate ships. If you've never left the island, when--
AVERY: Don't worry about it, Charlie. We should go and see Kell.
CHARLIE: Oooookay.
CHARLIE: You don't have to come with me. Didn't you have someone you wanted to see?
AVERY: Nah, no rush. I'll come with you.
->All right, let's go.
//:) for not questioning
    AVERY: Thanks, Charlie.
->Are you sure? 
    AVERY: Yeah, it's nothing important. I can hang with you a bit longer.
    CHARLIE: Okay then.
->Uhhh...Fine.
    AVERY: Wow, don't get too excited about it. 
//:(
AVERY: Have you visited Kell here before? Do you know where they live?
CHARLIE: Yeah, their house is just up ahead.
AVERY: Then let's go say hi.


===
title: KellsHouseExt
tags: 
colorID: 3
position: -499,-917
---
<<if $biStage is 1>>
CHARLIE: This is Kell's house.
->[Knock]
    [No answer]
    AVERY: Try knocking louder?
    ->[Knock louder]
        [No answer]
        AVERY: Maybe they're sleeping in.
        CHARLIE: Maybe. Let's go in.
        ->[Enter]
            <<Change Scene 7>>
    ->[Enter]
        <<Change Scene 7>>
->[Enter]
    <<Change Scene 7>>
<<endif>>
<<if $biStage is 2>>
    AVERY: We know Kell's not here. We should go check out what that noise is.
<<endif>>
<<if $biStage is 5>>
    MAISON: Are you sure we'll find anything in here?
    ->It's worth a look.
        MAISON: If you say so.
        <<Change Scene 7>>
    ->Probably not.
        CHARLIE: Maybe we'll come back later.
<<endif>>
===
title: KellsHouseInt
tags: 
colorID: 3
position: -283,-916
---
<<if $biStage is 1>>
CHARLIE: Kell? It's Charlie.
CHARLIE: Hello?
[no answer]
AVERY: I guess they're not home. Should we wait and see if they come back?
<<set $looked to false>>
<<set $biStage to 2>>
->Maybe just for a minute.
    AVERY: Cool. 
    AVERY: I wonder what Kell's been up to since they left Blossom Island.
    CHARLIE: Working, they said. I think they had a job in the mayor's office.
    AVERY: A politician, huh? Wonder if they've got any interesting dirt on the mayor lying around.
    AVERY: Want to look around?
    <<set $looked to true>>
->Let's look around town.
    <<Change Scene 6>>
<<else>>
    <<show Maison>>
    <<show Note>>
    AVERY: Let's look for anything that can prove the cult took Kell.
<<endif>>
===
title: Debate
tags: 
colorID: 4
position: 150,-913
---
<<move Charlie Debate>>
<<show Eden>>
<<move Eden Charlie>>
TAM: You're all smiles and empty words. You wouldn't last a day in office.
LUCA: What's so bad about a smile?
TAM: You're weak. You haven't got the guts to do what needs to be done.
LUCA: You make it all sound so dramatic. 
LUCA: Friends, thank you for coming today. I know that what you've heard has shown you that I am the best candidate.
LUCA: Perhaps I am weak, as Tam says. 
LUCA: But surely it's better to be weak than hard-headed and full of aggression, howling like a mad wolf hungry for violence.
TAM: What is THAT supposed to mean?
LUCA: Have a good day, everyone.
<<move Alvy AlvysHouseExt>>
<<move Sam SamsHouseExt>>
<<move Luca TownHallExt>>
CHARLIE: Hey, do you know what's going on?
EDEN: The last mayor of Berry Island recently passed away. 
<<move Tam TownHallExt>>
EDEN: Those two up there are hoping to be the next one.
CHARLIE: It looked like it was getting pretty heated.
EDEN: Yeah. That's politics for you.
EDEN: Are you two visiting? I haven't seen you around before.
CHARLIE: I'm here to see my sibling, Kell. 
CHARLIE: Do you know them? They weren't at home.
EDEN: Yes, I know Kell. They worked for the last mayor.
EDEN: I haven't seen them in a few days...but they mostly keep to themself.
EDEN: Was Kell expecting you?
CHARLIE: I'm here to surprise them for their birthday.
    //if you looked around the house
    //AVERY: It looked like no one had been home for a few days.
EDEN: Hmm...I wonder...
EDEN: Several people have gone missing in the last few weeks.
CHARLIE: Kell's gone missing?!
EDEN: We don't know that for sure. Why don't you ask around? Maybe someone's seen them.
EDEN: Come and find me at the police station if you find out anything.
<<move Eden PoliceStationExt>>
<<set $biStage to 3>>
<<show Maison>>


===
title: MaisonMeeting
tags: 
colorID: 2
position: 589,-915
---
MAISON: I knew they'd come after me sooner or later.
MAISON: Thanks a bunch, friends. Thought I was gone for sure.
CHARLIE: You're welcome. Are you Maison?
MAISON: Who's asking?
CHARLIE: I'm Charlie, and this is Avery. I'm looking for my sibling, Kell. 
CHARLIE: I think they've gone missing.
AVERY: Someone in town said you knew something about the disappearances. //people in town? specific person?
MAISON: Something? I know EVERYTHING! Come with me, I'll show you.
<<Change Scene 8>>

===
title: MaisonsHouseInt
tags: 
colorID: 2
position: 596,-701
---
<<if $biStage is 3>>
//standing in front of conspiracy board
MAISON: ...and that's why people are going missing.
CHARLIE: ...
AVERY: ...
CHARLIE: You think that one of the people going for mayor is kidnapping people?
AVERY: To EAT them.
CHARLIE: As part of some weird cult ritual?
MAISON: You saw the cultists try to get me, right? The cult is real. They're taking people.
MAISON: Think about it! Who has enough power in this town to make it all just go away?
MAISON: The mayor, that's who! That's why Tam is trying so hard to win votes.
MAISON: They want to turn this town into an all-you-can-eat buffet!
AVERY: Riiiiight...
AVERY: [muttering] Charlie, they're crazy. //let's get out of here
CHARLIE: I don't know...maybe they're on to something. We both saw those cultists.
AVERY: They think Tam is eating people!
CHARLIE: I didn't say I believed everything they said.
CHARLIE: Maison, have you told the police about this?
MAISON: Ha, those idiots? They're useless! 
MAISON: They wouldn't be able to solve a mystery like this if the culprit was dancing naked in front of them wearing a sombrero!
CHARLIE: Well, I was talking to one of them earlier and they said they would help me find Kell.
CHARLIE: Will you come with me and tell them what you told us?
AVERY: [whispering] This is a terrible idea.
MAISON: Go to the police? They won't believe me. They're too stupid to see the truth.
->Please? We need your help.
->You never know.
    CHARLIE: They might surprise you. ///???
->[mean option]
MAISON: ...Fine. Just let me grab a couple of things.
<<set $biStage to 4>>
<<Change Scene 6>>
<<else>>
    MAISON: Solved it. The cult meets in a cabin on the northeast side of the island.
    CHARLIE: Let's go check it out.
    <<set $biStage to 8>>
    <<Change Scene 6>>
<<endif>>

===
title: StationInt
tags: 
colorID: 1
position: 817,-693
---
<<if $biStage is 4>>
EDEN: Did you find Kell?
CHARLIE: No, but we found Maison. They think they know why people are going missing.
MAISON: Hello there, Officer Eden.
EDEN: [sigh] Hello, Maison. What is it this time? I told you, no one's been drugging the water.
MAISON: All those people who've gone missing? I figured it out. Before you.
MAISON: It's a cult taking them. A cult led by Tam.
EDEN: ...
EDEN: A cult.
MAISON: See, Charlie? I told you they wouldn't believe me.
CHARLIE: Maison's right - about the cult, anyway. We saw them.
EDEN: Listen, kid. I don't know you, but I know Maison. Their theories are always getting them into trouble.
EDEN: Don't let them drag you into this.
AVERY: I hate to agree with the conspiracy nut, but...we really did see the cult.
EDEN: [sigh] Even if I believed you...Cults aren't illegal. I can't arrest people for belonging to a cult.
EDEN: If there IS a cult, and if they ARE taking people, I need proof. Solid proof. Can you give me that?
->a
->b
->c
EDEN: Good luck. I'll keep an eye out for Kell.
<<set $biStage to 5>>
<<set $stage5 to "station">>
<<Change Scene 6>>
<<endif>>

<<if $biStage is 6>>
    [[PoliceStation2]]
<<endif>>

<<if $biStage is 9>>
    [[PoliceStation3]]
<<endif>>

===
title: StationExit
tags: 
colorID: 1
position: 816,-471
---
CHARLIE: How are we going to find proof?
AVERY: Maybe there's some evidence at Kell's house we missed.
MAISON: We should talk to Tam. Get them to confess.
AVERY: You don't have to come with us anymore, Maison.
MAISON: If you're hunting the cult, there's no way I'm staying behind.
MAISON: Besides, they'll be after all of us now that we're on their trail. We need to stick together. 
MAISON: There's safety in numbers.
AVERY: If you say so...
AVERY: It'll be a waste of time talking to Tam, though. If they are involved, they're not going to just tell us.
MAISON: It's a better idea than going to search some empty house.
AVERY: Guess it's up to you, Charlie.
===
title: TamStage5
tags: 
colorID: 6
position: -403,275
---
<<set $stage5 to "tam">>
TAM: What are you doing in here?
MAISON: We know what you're up to.
TAM: What I'm up to is running for office. If you're not here to talk about that, you should leave.
CHARLIE: We just have a few questions.
TAM: I don't have time for this. I don't know you, but I know your friend there. Always causing trouble.
TAM: You all need to--Wait. Avery? Is that you?
AVERY: Hi, Tam.
CHARLIE: Avery? You know Tam?
AVERY: No...
AVERY: Well. Yes.
MAISON: This is why you didn't want to come here! You're working with the enemy!
TAM: The enemy?
AVERY: I'm not working with Tam.
TAM: What are you talking about?
CHARLIE: How do you know each other? Why didn't you say anything before?
MAISON: Don't listen to them, Charlie, they're here to spy on us!
TAM: WILL SOMEONE TELL ME WHAT IS GOING ON!
->What do you know about the missing people?
    TAM: I know people are going missing. That's it.
    MAISON: They're lying. I know they're lying.
    TAM: First you break into my office, now you accuse me of being involved with the disappearances?
    TAM: Get out, before I call the police.
    AVERY: Tam--
    TAM: All of you.
    ->[Leave]
        CHARLIE: We're leaving. 
        <<Change Scene 6>>
    ->No. Not until you answer our questions.
        MAISON: Yeah! 
        TAM: Get. Out.
        CHARLIE: All right, all right, we're going.
        <<Change Scene 6>>
->We know you're part of the cult.
    TAM: [sigh] You've spent too long with this conspiracy nut.
    MAISON: Stop deflecting! We know you're guilty!
    TAM: All right, I've had enough of this. Get out of my office.
    CHARLIE: Tell me where Kell is, and we'll go.
    TAM: I don't know Kell. 
    TAM: You have three seconds to leave before I call the police.
    AVERY: Tam--
    TAM: Get. Out.
    CHARLIE: All right, we're leaving.
    MAISON: But we'll be back!
    <<Change Scene 6>>



===
title: TamExit
tags: 
colorID: 4
position: -133,324
---
MAISON: You! You're working with the cult!
AVERY: Calm down, crazy. I'm not working with anybody. 
MAISON: You know Tam! You didn't want us to question them!
->[defend avery]
    CHARLIE: It's all right, Maison. I trust Avery.
    MAISON: Trust them? After they lied to us?
    MAISON: I thought you were different, Charlie, but you're just as blind as everyone else.
    CHARLIE: I believe you about the cult, but I don't think Avery's a part of it.
    AVERY: I'm not.
    MAISON: How can you trust them, Charlie? They didn't tell us they knew Tam.
    CHARLIE: Avery? Will you tell us?
    
->[question avery]
    CHARLIE: Why didn't you tell us you knew Tam?
    AVERY: Because...it's weird, okay? I haven't seen them in years.
    CHARLIE: How do you know them?

->[accuse avery]
    CHARLIE: Maison's right. You defended Tam.
    AVERY: Really? You're siding with Maison?
    AVERY: Listen. I'm not part of any damn cult. And I'm pretty sure Tam isn't, either.
    MAISON: Prove it!
    AVERY: How am I supposed to prove it, huh? You obviously don't believe me.
    CHARLIE: Tell us how you know Tam.

[[TamSibling]]


    



===
title: TamSibling
tags: 
colorID: 4
position: 132,356
---
AVERY: ...
AVERY: Tam's my sibling.
CHARLIE: Why didn't you say something?
AVERY: I don't know. I thought...
AVERY: It doesn't matter. We need to focus on finding Kell.
AVERY: I swear, Charlie, I'm not part of the cult.
MAISON: This doesn't prove anything.
<<if visited("KellsHouse2") is true>>
    CHARLIE: Let's just go talk to Officer Eden.
    <<set $biStage to 6>>
<<else>>
    CHARLIE: Let's just go check out Kell's house.
<<endif>>
===
title: PoliceStation2
tags: 
colorID: 1
position: 820,-258
---
EDEN: Hello again. Did you find anything?
CHARLIE: We have proof the cult exists.
MAISON: And Avery is part of it.
AVERY: I'm not in the damn cult!
MAISON: But Tam is.
AVERY: No they aren't!
EDEN: Wait - Tam? Never mind, it doesn't matter.
EDEN: I told you, I can't go around arresting people just because they belong to a cult.
EDEN: I need more than that. Get me proof that they're kidnapping people, and then I can do something.
<<Change Scene 6>>

===
title: Hideout
tags: 
colorID: 0
position: 1254,-670
---
//people in cages - one? two?
MAISON: [w] It's the missing people!
CHARLIE: Are you sure?
MAISON: Absolutely. I snuck into Eden's office and took a photo of their files.
AVERY: Pretty sure that's illegal.
MAISON: So is trespassing.
AVERY: Fair point.
CHARLIE: Avery, can you take a photo? We need to bring back proof.
[footsteps]
MAISON: Someone's coming!
<<show Luca>>
<<show Devon>>
//walking into view in front of cages--luca! in cult clothes
//<<move Luca LucaTarget>>
//<<move Devon DevonTarget>>
LUCA: You'll take them to HQ tonight?
DEVON: As soon as it's dark.    
CHARLIE: LUCA is part of the cult? 
LUCA: Good. The ritual is almost ready.
MAISON: Quick, take a photo! 
LUCA: Did you hear something?
AVERY: Time to go.

//run back to other door.
//devon chases?
//or
<<set $biStage to 9>>
<<Change Scene 6>>
===
title: PoliceStation3
tags: 
colorID: 1
position: 838,182
---
MAISON: I was right! About everything!
AVERY: Not everything.
EDEN: What? What are you talking about?
CHARLIE: We found the cult's hideout. They're behind the missing people.
AVERY: Here, we got photos. [shows phone]
EDEN: [looks at photos] Those are definitely some of the missing people.
EDEN: Is that Luca?
MAISON: I told you so! I told you so!
MAISON: I said one of them belonged to the cult and you didn't believe me!
CHARLIE: They said they were leaving tonight. 
EDEN: Then we need to hurry. 
<<set $biStage to 10>>
<<Change Scene 6>>

===
title: HideoutExit
tags: 
colorID: 0
position: 1257,-451
---
MAISON: I knew it! I was right about the cult!
AVERY: You were wrong about Tam.
MAISON: We don't know they're NOT part of the cult.
AVERY: What would be the point of running against Luca for mayor if they're both part of the cult?
MAISON: So you don't suspect them. It's clearly working.
CHARLIE: We need to get the evidence to Eden before the cultists find us.
===
title: LucaStage10
tags: 
colorID: 2
position: 464,267
---
<<stop Luca>>
LUCA: Hello, Officer. And, er, friends. How can I help you?
EDEN: You're under arrest for the abduction and unlawful detainment[????]. //???????
LUCA: Under arrest? There must be some mistake.
MAISON: We know you're part of the cult! We have proof!
LUCA: Really, Officer Eden, you should know better than to listen to this one. 
LUCA: Haven't they caused you plenty of trouble in the past?
LUCA: I'm sure this is all a simple misunderstanding.
EDEN: They have proof, Luca. Are you going to come quietly?
LUCA: ... 
<<move Luca Rowboat>>
MAISON: Quick, after them!
//chase scene!!
//uh charlie says nothing in this scene...
===
title: Docks
tags: 
colorID: 5
position: 1505,-909
---
<<move EvilBoat Exit>>
EDEN: Damn it, they're getting away.
MAISON: Are there any other boats around?
CHARLIE: I might know someone who can help.
AVERY: Oh, no.
//fade to redpeel conversation?
//or go straight to final island and thank redpeel as you're leaving the dock there?
<<Change Scene 13>>
===
title: KellsDoor
tags: 
colorID: 3
position: -281,-701
---
<<if $biStage is 2>>
AVERY: Are we leaving?
->Yes.
    AVERY: Cool, let's go.
    <<Change Scene 6>>
->Not just yet.
<<else>>
    <<if visited("KellsNote") is true>>
        ->[Leave]
            CHARLIE: I think we've found what we need. 
            <<Change Scene 6>>
        ->[Keep looking]
            CHARLIE: I just want to look around for a bit longer.
    <<else>>
        AVERY: We haven't found anything yet. We should keep looking.
    <<endif>>
<<endif>>
===
title: testA
tags: 
colorID: 0
position: -1121,-608
---
CHARLIE: hey
===
title: KellsHouseExit
tags: 
colorID: 3
position: -69,-918
---
<<if $biStage is 2>>
<<show Devon>>
<<show Riley>>
<<show Luca>>
<<show Tam>>
<<show Bailey>>
<<show Ash>>
<<show Dee>>
<<show Sam>>
<<show Alvy>>

AVERY: Hey, do you hear that? There's something up ahead.
CHARLIE: I hear it. Let's go check it out.
<<else>>
    <<if visited("TamsDesk") is true>>
        <<set $biStage to 6>>
        CHARLIE: We should take all this evidence to Eden.
    <<else>>
        MAISON: Let's pay a visit to Tam.
        AVERY: But we already have proof that the cult exists.
        MAISON: And now we need proof that Tam is involved.
    <<endif>>
<<endif>>


===
title: DR
tags: 
colorID: 4
position: 369,-914
---
DEVON: Tam has a point. Just think about it. Luca talks a good game, but what have they actually done?
DEVON: At least Tam worked with the previous mayor, so they know what the job involves.
RILEY: Yeah, maybe...But Luca was right. Tam's so aggressive. I don't know if I want a mayor like that.
//aggressive???
CHARLIE: Do either of you know Kell?
DEVON: Who?
RILEY: Kell...Kell...Didn't they work for the old mayor?
CHARLIE: Yeah, I'm trying to find them.
RILEY: Can't help you, sorry. We should get back to work.
<<move Riley BankExt>>
<<move Devon BankExt>>

===
title: Alvy
tags: 
colorID: 4
position: 153,-493
---
CHARLIE: Excuse me, do you know Kell?
ALVY: Sorry can't talk, I think I left my oven on...
===
title: Sam
tags: 
colorID: 4
position: 152,-706
---
CHARLIE: Hey, do you know Kell?
SAM: GO AWAY CAN'T YOU SEE I'M IN A HURRY!
===
title: BA
tags: 
colorID: 4
position: 372,-489
---
BAILEY: What Luca said about Tam was totally uncalled for!
ASH: It was the truth and you know it. Luca would never say something like that if it wasn't true.
ASH: Oh, hi there.
BAILEY: Can we help you?
CHARLIE: I'm trying to find my sibling, Kell. Do you know them?
BAILEY: I know OF them. I've seen them around, but I don't think we've ever had a conversation.
ASH: Sorry, kid. But Bailey, seriously, Tam's only in it for the power.
===
title: Dee
tags: 
colorID: 4
position: 370,-705
---
CHARLIE: Do you know Kell?
DEE: Yes, I know them. Why do you ask?
CHARLIE: I'm trying to find them. 
DEE: I haven't seen them since the old mayor died a few days ago.
CHARLIE: I think they've gone missing.
DEE: Missing? You haven't been talking to Maison, have you?
DEE: That troublemaker was snooping around here the other day, claiming they were trying to find the missing people.
DEE: I'm sure Kell's just fine.
AVERY: Where can we find Maison?
DEE: They live in the cemetery. How they can stand to sleep in such a place, I'll never understand.
AVERY: Sounds like this Maison might have an idea about what's going on.
CHARLIE: Let's go find them.

===
title: Station
tags: 
colorID: 1
position: 814,-906
---
<<if $biStage is 4>>
    MAISON: Let's hope these dancing monkeys can see the truth.
    <<Change Scene 9>>
<<endif>>

<<if $biStage is 6>>
    MAISON: Now that we have proof, Officer Eden HAS to listen to us.
    ->Let's go.
        <<Change Scene 9>>
    ->I don't want to go in just yet.
        MAISON: The sooner we deal with the cult, the sooner they'll stopping taking people.
<<endif>>

<<if $biStage is 9>>
    CHARLIE: I hope Officer Eden's in.
    <<Change Scene 9>>
<<endif>>

//????
MAISON: We don't have enough to show Eden yet.
===
title: Tam
tags: 
colorID: 6
position: 155,-278
---
<<if $biStage is 3>>
    [[TamStage3]]
<<endif>>
<<if $biStage is 5>>
    [[OfficeStage5]]
<<endif>>
===
title: Luca
tags: 
colorID: 6
position: 370,-274
---
<<if $biStage is 3>>
    [[LucaStage3]]
<<endif>>


<<if $biStage is 10>>
    [[LucaStage10]]
<<endif>>
===
title: TamStage3
tags: 
colorID: 6
position: -141,17
---
<<if visited("TamStage3") is false>>
    TAM: Who are you?
    CHARLIE: My name's Charlie. I'm looking for my sibling, Kell. 
    CHARLIE: They used to work here.
    TAM: I know Kell. Haven't seen them in a few days.
    TAM: You should go. I'm very busy.
    ->[Luca's Office]
    ->[Leave]
<<else>>
    TAM: I told you to go.
    ->[Luca's Office]
        <<Change Scene 10>>
    ->[Leave]
        <<Change Scene 6>>
<<endif>>
===
title: LucaStage3
tags: 
colorID: 6
position: 374,14
---
<<if visited("LucaStage3") is false>>
    LUCA: Hello there! What can I do for you?
    CHARLIE: Do you know Kell? They worked here with the last mayor.
    LUCA: Yes, I know Kell. Not very well, unfortunately. They mostly kept to themself.
    LUCA: You look a little like them, in fact. Are you related?
    CHARLIE: Kell's my sibling.
    LUCA: Of course. I can see the similarities clear as day. 
    LUCA: It's wonderful to meet you - what was your name?
    CHARLIE: Charlie.
    LUCA: It's wonderful to meet you, Charlie. I never knew Kell had a sibling.
    CHARLIE: Do you know where they are? I'm trying to find them.
    LUCA: I'm sorry, but I'm afraid I can't help you. I haven't seen Kell for a few days.
    CHARLIE: Oh. Okay. Thanks anyway.
    ->[Tam's Office]
    ->[Leave]
<<else>>
    LUCA: I'm sorry I can't help you find Kell. Is there anything else I can do for you?
    CHARLIE: No thanks.
    ->[Tam's Office]
        <<Change Scene 11>>
    ->[Leave]
        <<Change Scene 6>>
<<endif>>
===
title: TownHall
tags: 
colorID: 0
position: -739,-437
---
->[Tam's Office]
    <<set $office to "tam">>
    [[AveryTownHall]]
->[Luca's Office]
    <<set $office to "luca">>
    [[AveryTownHall]]
->[Come back later]


===
title: AveryTownHall
tags: 
colorID: 0
position: -480,-436
---
<<if $biStage is 4>>
<<if visited("AveryTownHall") is false>>
AVERY: I think I'll just...wait out here.
->Why?
    AVERY: Uhh...
    AVERY: Politicians, you know.
    CHARLIE: All right, I'll be back soon.
->Are you sure?
    AVERY: Yeah, go right ahead.
    CHARLIE: All right, I'll be back soon.
->Do what you want.
    AVERY: I will.
<<else>>
AVERY: I'll wait out here.
<<endif>>
<<endif>>

<<if $biStage is 5>>
AVERY: I really don't think this is necessary. I bet Tam doesn't know anything.
MAISON: Why are you so against talking Tam? What are you hiding?
AVERY: Nothing! 
AVERY: Let's just go.
<<endif>>

<<if $office is "tam">>
    <<Change Scene 11>>
<<else>>
    <<Change Scene 10>>
<<endif>>
===
title: TownHallExit
tags: 
colorID: 0
position: -480,-215
---
AVERY: How'd it go?
CHARLIE: No one knows where Kell is. It's like they just disappeared.
AVERY: There must be someone who knows what happened to them.
===
title: KellsPlants
tags: 
colorID: 3
position: -502,-1136
---
<<if $biStage is 2>>
    CHARLIE: These plants look like they haven't been watered recently. 
<<else>>
    MAISON: Is that--that's belladonna.
    AVERY: Bella-what?
    MAISON: Belladonna. It's a poisonous plant.
    CHARLIE: Kell was always growing weird plants back when they lived on Blossom Island.
<<endif>>

===
title: KellsKitchen
tags: 
colorID: 3
position: -282,-1136
---
<<if $biStage is 2>>
    CHARLIE: There's no food here...
<<else>>
    MAISON: We're not here to eat, we're here to look for proof!
    MAISON: Although I could really go for a sandwich...
<<endif>>

===
title: KellsLab
tags: 
colorID: 3
position: -64,-1137
---
<<if $biStage is 2>>
    CHARLIE: What's all this?
    AVERY: Looks like some kind of lab equipment.
    CHARLIE: I don't understand any of these notes...Are they formulas?
<<else>>
    MAISON: Fascinating...
    CHARLIE: Did you find anything?
    MAISON: These notes are full of formulas for different elixirs and concoctions. It's all in shorthand though, I can't tell what they're for.
    AVERY: Did you find anything about the cult?
    MAISON: Oh. Well. No.
    MAISON: But these notes are really interesting. I could spend days deciphering them!
    CHARLIE: Let's keep looking.
<<endif>>

===
title: AveryBerry
tags: 
colorID: 0
position: -1348,-604
---
AVERY: What's up?
===
title: OfficeStage5
tags: 
colorID: 6
position: 128,110
---
CHARLIE: Tam's not here.
MAISON: Quick, let's search their office before they get back.
===
title: TamsDesk
tags: 
colorID: 6
position: -402,34
---
CHARLIE: Look, I found a note.
MAISON: What does it say, what does it say?
CHARLIE: "Meeting tonight at dusk. You know where. Don't let anyone see you."
MAISON: PROOF! 
MAISON: I told you they were part of the cult!
AVERY: We don't know that for sure. That note could mean anything.
MAISON: No it couldn't! 
->Avery's right.
    CHARLIE: It's suspicious, but it doesn't say anything about a cult.
    AVERY: Told you.
->We still need more evidence.
    CHARLIE: Let's keep looking before we jump to any conclusions.
->Maison's right.
    CHARLIE: This proves that Tam's involved with the cult.
    MAISON: I knew you were a smart one, Charlie.
<<position Tam Entry>>
[[TamStage5]]
===
title: Stage5
tags: 
colorID: 0
position: -1481,-161
---
<<if $stage5 is "tam">>
    [[TamExit]]
<<endif>>

<<if $stage5 is "station">>
    [[StationExit]]
<<endif>>

<<if $stage5 is "KellsHouse">>
    [[KellsHouseExit]]
<<endif>>



    
===
title: KellsNote
tags: 
colorID: 3
position: -499,-698
---
CHARLIE: I think there's something under the rug.
AVERY: It looks like some kind of note.
CHARLIE: It's written in code. I can't read it.
MAISON: Let me see. [takes the note]
<<Take Note>>
MAISON: It'll take me a while to decode it. But here, look, it's the cult symbol.
MAISON: It's proof that the cult exists! 
CHARLIE: And that they took Kell.
CHARLIE: Let's go.
<<set $stage5 to "KellsHouse">>
===
title: Stage6
tags: 
colorID: 1
position: 825,-38
---
MAISON: I told you involving the police was a waste of time.
CHARLIE: We just need to find something connecting the cult to the disappearances.
AVERY: Where do we even start looking?
MAISON: Maybe there's something in the note we found at Kell's. 
MAISON: Let's go back to my place. I can decode it.
<<set $biStage to 7>>
->All right, let's go.
    AVERY: Back to the creepy graveyard, then...
    <<Change Scene 8>>
->Not just yet.
    
===
title: BERRYNOTES
tags: 
colorID: 0
position: -1331,-1062
---
Berry Island: 6
Kell's House: 7
Maison's House: 8
Station: 9
Luca's Office: 10
Tam's Office: 11
Cult hideout cabin: 12

Stage 1:
Stage 2:
Stage 3:
Stage 4: after maison's conspiracy talk, talking to eden (set in maisonshouseint)
Stage 5: (set in stationint) finding evidence 
Stage 6: after visiting both Tam and Kell's house to find evidence, lasts until conversation after exiting station
Stage 7: after bringing evidence of cult to Eden and setting out to look for proof that cult is involved in disappearances (set in "Stage6")
Stage 8: after (Maison) has solved the puzzle (set in MaisonsHouseInt)
Stage 9: after seeing Luca and Devon in cult hidout. (set in Hideout)
Stage 10: after telling Eden about Luca (set in PoliceStation3)
===
title: MaisonsHouse
tags: 
colorID: 2
position: 596,-474
---
<<if $biStage is 7>>
MAISON: I should be able to decipher the code using my notes on the cult.
->Let's go.
    <<Change Scene 8>>
->Wait a minute.
<<endif>>


//<<if $biStage lo 4>>
    CHARLIE: The door is locked.
    AVERY: Is that a keypad?
    CHARLIE: Yep, and a thumbprint scanner.
    AVERY: Whoever lived here sure didn't skimp on security.
//<<endif>>

//<<if $biStage le
===
title: CabinFrontDoor
tags: 
colorID: 0
position: 1030,-907
---
<<if visited("CabinFrontDoor") is false>>
MAISON: Wait, we can't just charge through the front door.
AVERY: Why not?
MAISON: Because it lacks both subtlety and sense. Neither of which you possess, Avery. //too formal?
MAISON: Unless you want us to get caught by the cultists who are undoubtedly on the other side. Because you're working with the cult.
AVERY: Not this again.
AVERY: We can just go through the front door and deal with whoever we find.
MAISON: Or we could do the smart thing and find a different entrance. 
MAISON: Barging through the front door is a good way to get us killed.
CHARLIE: We probably should try not to draw too much attention.
CHARLIE: Let's try to find another way in.
<<else>>    //if already had discussion
MAISON: We need to find another way in.
CHARLIE: Let's keep looking.
<<endif>>

===
title: CabinSideDoor
tags: 
colorID: 0
position: 1032,-683
---
<<if visited("CabinSideDoor") is false>>
MAISON: This will do. //seems quiet
AVERY: Ready, Charlie?
<<else>>
    MAISON: Ready to see what's inside?
<<endif>>
->Let's go.
    MAISON: Time to prove what the cult's up to once and for all.
    <<Change Scene 12>>
->Wait a moment.
    CHARLIE: I'm not ready to go in just yet.
===
title: CabinInt
tags: 
colorID: 0
position: 1034,-469
---
CHARLIE: Let's see if we can find anything that links the cult to the missing people.
[[CabinDoor2]]
===
title: CabinDesk
tags: 
colorID: 0
position: 1037,-247
---
AVERY: Anything interesting?
CHARLIE: I think these are take-out menus...
===
title: CabinDoor1
tags: 
colorID: 0
position: 1047,-36
---
CHARLIE: It's locked.
===
title: Crates
tags: 
colorID: 0
position: 1051,185
---
CHARLIE: [smashes the crates]
MAISON: Hopefully no one heard that.
===
title: CabinDoor2
tags: 
colorID: 0
position: 1254,-908
---
CHARLIE: I think I hear something on the other side...
AVERY: Let's go, then.
Maison: Quietly.
//open door
//move them through door
[[Hideout]]
===
title: Stage10
tags: 
colorID: 1
position: 840,395
---
<<show Luca>>
<<position Luca CabinExt>>
<<move Luca TownHallExt>>
EDEN: Where did you see Luca last?
CHARLIE: The cabin.
EDEN: Take me there, quickly.
===
