title: BerryIslandStart
tags: 
colorID: 5
position: -720,-917
---
<<setFollow Maison false>>
CAPTAIN REDPEEL: Here we are. Berry Island.
CAPTAIN REDPEEL: You kids been here before?
CHARLIE: Not for a while.
CAPTAIN REDPEEL: Well then, here's a map in case you get lost.
AVERY: All right, let's go and see Kell.
CHARLIE: You don't have to come with me. Didn't you have someone here you wanted to see?
AVERY: Well, yes, but. You know. No rush. I'll come with you.
CHARLIE: ...If you're sure.
//Options removed for now. pointless unless friendship meter is implemented
//->All right, let's go.
//:) for not questioning
    //AVERY: Thanks, Charlie.
//->Are you sure? 
    //AVERY: Yeah, it's nothing important. I can hang with you a bit longer.
    //CHARLIE: Okay then.
//->Uhhh...Fine.
   //AVERY: Wow, don't get too excited about it. 
//:(
AVERY: Have you visited Kell here before? Do you know where they live?
CHARLIE: Yeah, their house is just up ahead.
AVERY: Then let's go say hi.
<<show Hinty true>>
HINTY: Hey Charlie! I'm just dropping by to let you know that you can press Y to see your map.
CHARLIE: Did anyone else hear that?
AVERY: Hear what?
CHARLIE: ...Never mind.
HINTY: Bye Charlie! Have fun!
<<Take Hinty>>



===
title: KellsHouseExt
tags: 
colorID: 3
position: -499,-917
---
<<if $biStage is 1>>
CHARLIE: This is Kell's house.
->[Knock]
    HOUSE: ...
    AVERY: Try knocking louder?
    ->[Knock louder]
        HOUSE: ...
        AVERY: Maybe Kell's sleeping in.
        CHARLIE: Well, time for them to wake up.
        ->[Enter]
            <<Change Scene 7>>
    ->[Enter]
        <<Change Scene 7>>
->[Enter]
    <<Change Scene 7>>
<<endif>>

<<if $biStage is 2>>
    AVERY: We know Kell's not here. We should go check out what that noise is.
<<endif>>

<<if $biStage is 3>>
    AVERY: Kell's not here. We should try to find someone who knows where they are.
<<endif>>

<<if $biStage is 5>>
    <<if $note is false>>
        MAISON: Are you sure we'll find anything in here?
        CHARLIE: It's worth a look.
        MAISON: If you say so.
        <<Change Scene 7>>
    <<else>>
        AVERY: We've already searched Kell's house.
        <<if $tam is false>>
            MAISON: We should investigate Tam.
        <<else>>
            AVERY: Let's take the evidence back to Eden.
        <<endif>>
    <<endif>>
<<endif>>

<<if $biStage is 6>>
    AVERY: We should take the evidence back to Eden.
<<endif>>

<<if $biStage is 4 or $biStage is 7 or $biStage is 8 or $biStage is 9 or $biStage is 10>>
    AVERY: We don't need to go into Kell's house right now.
<<endif>>




===
title: KellsHouseInt
tags: 
colorID: 3
position: -283,-916
---
<<if $biStage is 1>>
CHARLIE: Kell? Hello?
CHARLIE: Looks like they're not home.
AVERY: What now?
<<set $looked to false>>
<<set $biStage to 2>>
->Let's wait here for a bit.
    AVERY: I wonder what Kell's been up to since they left Blossom Island.
    CHARLIE: Working, they said. I think they had a job in the mayor's office.
    AVERY: A politician, huh? Wonder if they've got any interesting dirt on the mayor lying around... 
    <<set $looked to true>>
->Let's see if Kell's in town.
    <<Change Scene 6>>
<<else>>
    <<show Maison false>>
    <<show Note true>>
    AVERY: Let's look for anything that can prove the cult took Kell.
<<endif>>
===
title: Debate
tags: 
colorID: 4
position: 151,-921
---

//<<move Charlie Eden no>>

TAM: You're all smiles and empty words. You wouldn't last a day in office.
LUCA: What's so bad about a smile?
TAM: You're weak. You haven't got the guts to do what needs to be done.
LUCA: Oh, so dramatic! 
<<lookAt Luca Bailey>> 
LUCA: Friends, thank you for coming today. I know that what you've heard has shown you that I am the best candidate.
LUCA: Perhaps I am weak, as Tam says. 
LUCA: But surely it's better to be weak than hard-headed and full of aggression, howling like a mad wolf hungry for violence.
TAM: What is THAT supposed to mean?
LUCA: Have a good day, everyone.
<<move Alvy BankExt destroy>>
<<move Sam SamsHouseExt destroy>>
<<move Luca TownHallDoor destroy>>
<<lookAt Bailey Ash>>
<<lookAt Ash Bailey>>
<<lookAt Devon Riley>>
<<lookAt Riley Devon>>
AVERY: What was all that about?
CHARLIE: Let's ask.
<<lookAt Tam TownHallDoor>>
<<move Tam TownHallDoor destroy>>
<<lookAt Eden Charlie>>
<<move Charlie Eden no>>
CHARLIE: Hey, do you know what's going on?
EDEN: The last mayor of Berry Island recently passed away. 
EDEN: Those two up there are hoping to be the next one.
CHARLIE: It looked like it was getting pretty heated.
EDEN: Yeah. That's politics for you.
AVERY: You didn't already have another mayor lined up?
EDEN: The mayor's death was...unexpected.
EDEN: Are you two visiting? I haven't seen you around before.
CHARLIE: I'm here to see my sibling, Kell. Do you know them?
EDEN: Yes, I know Kell. They worked for the last mayor.
EDEN: I haven't seen them since the mayor passed...but they mostly keep to themself.
EDEN: Was Kell expecting you?
CHARLIE: Yeah, I'm here to see them for their birthday.
<<if $looked is true>>
    AVERY: We were just at their house, and it looked like no one had been home for a few days.
<<endif>>
EDEN: Hmm...I wonder...
EDEN: Several people have gone missing in the last few weeks.
CHARLIE: Kell's gone missing?!
EDEN: We don't know that for sure. Why don't you ask around? Maybe someone's seen them.
EDEN: Come and find me at the police station if you find out anything.
<<move Eden PoliceStationExt destroy>>
<<set $biStage to 3>>



===
title: MaisonMeeting
tags: 
colorID: 2
position: 589,-915
---
//this should run after the combat, so shouldn't be called by talking to maison, but while it is:

<<if $biStage is 3>>
MAISON: I knew they'd come after me sooner or later.
MAISON: Thanks a bunch, friends. Thought I was gone for sure.
CHARLIE: You're welcome. Are you Maison?
MAISON: Who's asking?
CHARLIE: I'm Charlie, and this is Avery. I'm looking for my sibling, Kell. 
CHARLIE: I think they've gone missing.
AVERY: Someone in town said you knew something about the disappearances. 
MAISON: Something? I know EVERYTHING! Come with me, I'll show you.
<<Change Scene 8>>
<<else>>
    [[MaisonBerry]]
<<endif>>

===
title: MaisonsHouseInt
tags: 
colorID: 2
position: 594,-701
---
<<if $biStage is 3>>
//standing in front of conspiracy board
MAISON: ...and that's why people are going missing.
CHARLIE: ...
AVERY: ...
CHARLIE: You think that one of the people going for mayor is kidnapping people?
AVERY: To EAT them.
CHARLIE: As part of some weird cult ritual?
//following line only if coming from combat
//MAISON: You saw the cultists try to get me, right? 
MAISON: The cult is real. They're taking people. 
MAISON: Think about it! Who has enough power in this town to make it all just go away?
MAISON: The mayor, that's who! That's why Tam is trying so hard to win votes.
MAISON: They want to turn this town into an all-you-can-eat buffet!
AVERY: Riiiiight.
AVERY: [muttering] That's one mad mango. 
//let's get out of here
CHARLIE: I don't know...maybe they're on to something. No one else seems to know where Kell is.
//We both saw those cultists. ONLY IF COMBAT
AVERY: They think Tam is eating people!
CHARLIE: I didn't say they were right about everything.
CHARLIE: Maison, have you told the police about this?
MAISON: Ha, those idiots? They're useless! They can't even solve the mayor's murder!
MAISON: They couldn't see the truth even if it was dancing naked in front of them wearing a sombrero!
AVERY: Wait, the mayor was murdered?
MAISON: Of course they were! Weren't you listening?
AVERY: I tuned out after the first ten minutes.
CHARLIE: I talked to an officer earlier and they said they would help me find Kell.
CHARLIE: Will you come with me and tell them what you told us?
//AVERY: [whispering] This is a terrible idea.
MAISON: Go to the police? They won't believe me. They're too busy polishing their badges and dancing on strings like the political puppets they are.
//political?
CHARLIE: We believe you - 
AVERY: I don't.
CHARLIE: - so maybe they will too.
//->Please? We need your help.
//->You never know.
   // CHARLIE: They might surprise you. ///???
//->[mean option]
MAISON: ...Fine. I suppose it could be entertaining. Just let me grab my knife and fork.
AVERY: ...Your what?
<<set $biStage to 4>>
<<Change Scene 6>>
<<else>>
    MAISON: Solved it. The cult meets in a cabin on the northeast side of the island.
    CHARLIE: Let's go check it out.
    <<set $biStage to 8>>
    <<Change Scene 6>>
<<endif>>

===
title: StationInt
tags: 
colorID: 1
position: 820,-693
---
<<if $biStage is 4>>
EDEN: Did you find Kell?
CHARLIE: No, but we found Maison. They think they know why people are going missing.
MAISON: Hello there, Officer Eden.
EDEN: [sigh] Hello, Maison. What is it this time? I've already told you, no one's been drugging the water.
MAISON: All those people who've gone missing? I figured it out. Before you.
MAISON: It's a cult taking them. A cult led by Tam.
EDEN: ...
EDEN: A cult.
MAISON: See, Charlie? I told you they wouldn't believe me.
CHARLIE: Just hear them out, please. No one else knows where Kell is.
//only use this line if combat 
//CHARLIE: Maison's right - about the cult, anyway. We saw them.
EDEN: Listen, kid. I don't know you, but I know Maison. Their theories are always getting them into trouble.
EDEN: Don't let them drag you into this.
//AVERY: I hate to agree with the conspiracy nut, but...we really did see the cult.
MAISON: Have you got any other leads on the disappearances?
EDEN: ...
MAISON: I didn't think so.
EDEN: [sigh] Even if I believed you...Cults aren't illegal. I can't arrest people for belonging to a cult.
EDEN: If there IS a cult, and if they ARE taking people, I need proof. Solid proof. Can you give me that?
CHARLIE: Absolutely.
EDEN: Good luck. I'll keep an eye out for Kell.
<<set $biStage to 5>>
<<set $stage5 to "station">>
<<Change Scene 6>>
<<endif>>

<<if $biStage is 6>>
    [[PoliceStation2]]
<<endif>>

<<if $biStage is 9>>
    [[PoliceStation3]]
<<endif>>

===
title: StationExit
tags: 
colorID: 1
position: 816,-471
---
CHARLIE: How are we going to find proof?
AVERY: Maybe there's some evidence at Kell's house we missed.
MAISON: We should talk to Tam. Get them to confess.
AVERY: You don't have to come with us anymore, Maison.
MAISON: If you're hunting the cult, there's no way I'm staying behind.
MAISON: Besides, they'll be after all of us now that we're on their trail. We need to stick together. 
MAISON: There's safety in numbers.
AVERY: If you say so...
AVERY: It'll be a waste of time talking to Tam, though. If they are involved, they're not going to just tell us.
MAISON: It's a better idea than going to search some empty house.
AVERY: Guess it's up to you, Charlie.
<<set $note to false>>
<<set $tam to false>>

<<position Ash Apartments>>
<<show Ash false>>
<<move Ash BankExt destroy>>

<<position Alvy AlvysHouseExt>>
<<show Alvy false>>
<<move Alvy Cinema destroy>>
<<set $alvy5 to false>>

<<position Sam SamsHouseExt>>
<<show Sam false>>
<<set $samJaime to false>>

//<<position Devon TownHallExt>>
//<<show Devon false>>
//<<move Devon CabinExt no>>
===
title: TamStage5
tags: 
colorID: 6
position: -405,275
---
<<set $stage5 to "tam">>
<<set $tam to true>>
TAM: What are you doing in here?
MAISON: We know what you're up to.
TAM: What I'm up to is running for office. If you're not here to talk about that, you should leave.
CHARLIE: We just have a few questions.
TAM: I don't have time for this. I don't know you, but I know your friend there. Always causing trouble.
TAM: You all need to - Wait. Avery? Is that you?
AVERY: Hi, Tam.
CHARLIE: Avery? You know Tam?
AVERY: No...
AVERY: Well. Yes.
MAISON: This is why you didn't want to come here! You're working with the enemy!
TAM: The enemy?
AVERY: I'm not working with Tam.
TAM: What are you talking about?
CHARLIE: How do you know each other? Why didn't you say anything before?
MAISON: Don't listen to them, Charlie, they're here to spy on us!
TAM: WILL SOMEONE TELL ME WHAT IS GOING ON!
CHARLIE: What do you know about the missing people?
TAM: I know people are going missing. That's it.
MAISON: They're lying. I know they're lying. Tell us what you know!
TAM: All right, I've had enough of this. Get out of my office.
CHARLIE: Tell me where Kell is, and we'll go.
TAM: I don't know where Kell is. 
TAM: You have three seconds to leave before I call security.
AVERY: Tam - 
TAM: All of you.
CHARLIE: All right, we're leaving.
MAISON: But we'll be back!
<<Change Scene 6>>





===
title: TamExit
tags: 
colorID: 4
position: -133,324
---
MAISON: You! You're working with the cult!
AVERY: Calm down, crazy. I'm not working with anybody. 
MAISON: You know Tam! You didn't want us to question them!
->[defend avery]
    CHARLIE: It's all right, Maison. I trust Avery.
    MAISON: Trust them? After they lied to us?
    MAISON: I thought you were different, Charlie, but you're just as blind as everyone else.
    CHARLIE: I believe you about the cult, but I don't think Avery's a part of it.
    AVERY: I'm not.
    MAISON: How can you trust them, Charlie? They didn't tell us they knew Tam.
    CHARLIE: Avery? 
    
->[question avery]
    CHARLIE: Why didn't you tell us you knew Tam?
    AVERY: Because...it's weird, okay? I haven't seen them in years.
    CHARLIE: How do you know them?

->[accuse avery]
    CHARLIE: Maison's right. You defended Tam.
    AVERY: Really? You're siding with Maison?
    AVERY: Listen. I'm not part of any damn cult. And I'm pretty sure Tam isn't, either.
    MAISON: Prove it!
    AVERY: How am I supposed to prove it, huh? You obviously don't believe me.
    CHARLIE: Tell us how you know Tam.

[[TamSibling]]


    



===
title: TamSibling
tags: 
colorID: 4
position: 132,356
---
AVERY: ...
AVERY: Tam's my sibling. They're the reason I came back to Berry Island.
CHARLIE: Why didn't you say something?
AVERY: I don't know. I thought...I thought they'd want to see me. It's been years, but I guess...
AVERY: Whatever. It doesn't matter. We need to focus on finding Kell.
AVERY: I swear, Charlie, I'm not part of the cult.
MAISON: This doesn't prove anything.
<<if $note is true>>
    CHARLIE: Let's just go talk to Officer Eden.
    <<set $biStage to 6>>
    <<if $samJaime is true>>
        <<position Sam PoliceStationExt>>
        <<show Sam false>>
    <<endif>>
<<else>>
    CHARLIE: Let's just go check out Kell's house.
    <<if $samJaime is false>>
        <<position Sam SamsHouseExt>>
        <<show Sam false>>
    <<endif>>
<<endif>>


<<position Ash BankExt>>
<<show Ash false>>
<<move Ash Apartments destroy>>

<<position Bailey SamsHouseExt>>
<<show Bailey false>>
<<move Bailey TownHallExt destroy>>

===
title: PoliceStation2
tags: 
colorID: 1
position: 820,-258
---
EDEN: Hello again. Did you find anything?
CHARLIE: We have proof the cult exists.
MAISON: And Avery is part of it.
AVERY: I'm not in the damn cult!
MAISON: But Tam is.
AVERY: No they aren't!
EDEN: Wait - Tam? Never mind, it doesn't matter.
EDEN: I told you, I can't go around arresting people just because they belong to a cult.
EDEN: I need more than that. Get me proof that they're kidnapping people, and then I can do something.
<<Change Scene 6>>

===
title: Hideout
tags: 
colorID: 0
position: 1251,-668
---
<<lookAt Charlie LucaTarget>>
MAISON: Those are some of the missing people!
CHARLIE: Are you sure?
MAISON: Absolutely. I snuck into Eden's office and took a photo of their files.
AVERY: I'm pretty sure that's illegal.
MAISON: So is trespassing.
AVERY: Fair point.
CHARLIE: I don't see Kell anywhere.
MAISON: There are other missing people who aren't here. The cult must have them stashed somewhere else.
MAISON: Unless they've already eaten them.
AVERY: Shut up, Maison. I'm sure Kell hasn't been eaten.
CHARLIE: ...
CHARLIE: Avery, can you take a photo? We need to bring back proof.
DOOR: [footsteps]
MAISON: Someone's coming!
<<open Door2>>
<<show Luca true>>
<<show Devon true>>
<<move Luca LucaTarget no>>
<<move Devon DevonTarget no>>
LUCA: You'll take them to HQ tonight?
<<lookAt Luca Devon>>
DEVON: As soon as it's dark.    
CHARLIE: LUCA is part of the cult? 
LUCA: Good. The ritual is almost ready.
MAISON: Quick, take a photo! 
LUCA: Did you hear something?
AVERY: Time to go.

//run back to other door.
//devon chases?
//or
<<set $biStage to 9>>
<<Change Scene 6>>
===
title: PoliceStation3
tags: 
colorID: 1
position: 838,182
---
MAISON: I was right! About everything!
AVERY: Not everything.
EDEN: What are you talking about?
CHARLIE: We found the cult's hideout. They're the ones who have been kidnapping people.
AVERY: Here, look at these photos. 
EDEN: ...
EDEN: Those are definitely some of the missing people.
EDEN: Is that Luca?
MAISON: I told you so! I told you so!
MAISON: I said one of them belonged to the cult and you didn't believe me!
CHARLIE: They said they were leaving tonight. 
EDEN: Then we need to hurry. 
<<set $biStage to 10>>
<<Change Scene 6>>

===
title: HideoutExit
tags: 
colorID: 0
position: 1257,-450
---
MAISON: I knew it! I was right about the cult!
AVERY: You were wrong about Tam.
MAISON: We don't know they're NOT part of the cult.
AVERY: What would be the point of running against Luca for mayor if they're both part of the cult?
MAISON: So you don't suspect them. It's clearly working.
CHARLIE: Those cultists were right behind us. We need to get the evidence to Eden before they catch up.

<<position Ash BankExt>>
<<show Ash false>>

<<position Bailey Cinema>>
<<show Bailey false>>
===
title: LucaStage10
tags: 
colorID: 2
position: 464,267
---
EDEN: You're under arrest, Luca.
LUCA: Under arrest? There must be some mistake.
MAISON: We know you're part of the cult! We know you've been kidnapping people!
LUCA: Really, Officer Eden, you should know better than to listen to this troublemaker. Haven't they caused you enough problems?
EDEN: They have proof, Luca. Are you going to come quietly?
LUCA: ... 
<<move Luca Rowboat no>>
MAISON: Quick, after them!
<<set $luca10 to true>>




===
title: Docks
tags: 
colorID: 5
position: 1505,-909
---
<<move EvilBoat Exit destroy>>
EDEN: Damn it, they're getting away.
MAISON: Are there any other boats around?
CHARLIE: I might know someone who can help.
//fade to redpeel conversation?
//or go straight to final island and thank redpeel as you're leaving the dock there?
<<Change Scene 16>>
===
title: KellsDoor
tags: 
colorID: 3
position: -281,-701
---
<<if $biStage is 2>>
AVERY: Are we leaving?
->Yes.
    AVERY: Cool, let's go.
    <<Change Scene 6>>
->Not just yet.
<<else>>
    <<if $note is true>>
        CHARLIE: I think we've found what we need. Let's go.
        <<Change Scene 6>>
    <<else>>
        AVERY: We haven't found anything yet. We should keep looking.
    <<endif>>
<<endif>>
===
title: testA
tags: 
colorID: 0
position: -1126,-374
---
CHARLIE: hey
===
title: KellsHouseExit
tags: 
colorID: 3
position: -63,-922
---
<<if $biStage is 2>>
<<setFollow Maison false>>
<<show Eden false>>
<<show Devon true>>
<<show Riley true>>
<<show Luca true>>
<<show Tam true>>
<<show Bailey true>>
<<show Ash true>>
<<show Dee true>>
<<show Sam true>>
<<show Alvy true>>
<<lookAt Bailey TownHallExt>>
<<lookAt Ash TownHallExt>>
<<lookAt Devon TownHallExt>>
<<lookAt Riley TownHallExt>>
<<lookAt Sam TownHallExt>>
<<lookAt Alvy TownHallExt>>
<<lookAt Eden TownHallExt>>

AVERY: Hey, do you hear that? There's something up ahead. 
CHARLIE: I hear it. It sounds like it's coming from near the town hall.
AVERY: Let's go check it out.
//<<move Eden Debate no>>

<<else>>
    <<if $tam is true>>
        <<set $biStage to 6>>
        CHARLIE: We should take all this evidence to Eden.
        <<if $samJaime is true>>
            <<position Sam PoliceStationExt>>
            <<show Sam false>>
        <<endif>>
    <<else>>
        MAISON: Let's pay a visit to Tam.
        AVERY: But we already have proof that the cult exists.
        MAISON: And now we need proof that Tam is involved. 
        <<if $samJaime is false>>
            <<position Sam SamsHouseExt>>
            <<show Sam false>>
        <<endif>>
    <<endif>>

    
    <<show Riley true>>
    <<show Dee true>>
    <<lookAt Riley Dee>>
    <<lookAt Dee Riley>>

<<endif>>


===
title: DR
tags: 
colorID: 4
position: 369,-914
---
<<if $biStage is 3>>
    <<if visited("DR") is false>>
    RILEY: Tam has a point. Just think about it. Luca talks a good game, but what have they actually done?
    RILEY: At least Tam worked with the last mayor, so they know what the job involves.
    DEVON: The last mayor was useless. Luca's right - Tam's unreliable. I don't want a mayor like that.
    CHARLIE: Do either of you know Kell?
    DEVON: Who?
    RILEY: Kell...Kell...Didn't they work for the last mayor?
    CHARLIE: Yeah, I'm trying to find them.
    RILEY: Can't help you, sorry. We should get back to work.
    <<move Riley BankExt destroy>>
    <<move Devon BankExt destroy>>
    <<else>>
        DEVON: We have to get back to work.
    <<endif>>
<<endif>>

<<if $biStage is 4>>
    <<if visited("DR") is false>>
        DEVON: Do we know you?
        CHARLIE: I'm from Blossom Island, I'm here to see - 
        DEVON: So we don't know you. Go away.
        <<lookAt Devon Riley>>
        <<lookAt Riley Devon>>
    <<else>>
        DEVON: You again. And you've got Maison with you.
        MAISON: I know you're planning to rig the election, Devon. You're not going to get away with it, I swear!
        DEVON: I'm going to rig the election. Sure. 
        <<lookAt Devon Riley>>
        DEVON: Now, Riley, where were we?
    <<endif>> 
<<endif>>
===
title: Alvy
tags: 
colorID: 4
position: 150,-496
---
<<if $biStage is 2>>
    ALVY: Just a moment. I'd like to listen.
<<endif>>


<<if $biStage is 3>>
CHARLIE: Excuse me, do you know Kell?
ALVY: Sorry, can't talk, think I left my oven on...
<<endif>>

<<if $biStage is 5>>
    <<stop Alvy>>    
    ALVY: Hello there, stranger. What can I do for you?
    CHARLIE: I'm looking for evidence that a cult is kidnapping people.
    ALVY: Oh...Well. Um, I'm afraid I can't help you with that. Uh, good luck?
    <<move Alvy Cinema destroy>>
    <<set $alvy5 to true>>
<<endif>>

<<if $biStage is 8>>
    <<stop Alvy>>
    <<if $alvy5 is true>>
        ALVY: Uh, hello again. How goes your search for the, um, cult?
        CHARLIE: We just need a bit more proof.
        ALVY: Well, uh, best of luck.
        <<move Alvy BankExt destroy>>
    <<else>>
        ALVY: Hello there, stranger. What can I do for you?
        CHARLIE: I'm looking for evidence that a cult is kidnapping people.
        ALVY: Oh...Well. Um, I'm afraid I can't help you with that. Uh, good luck?
        <<move Alvy BankExt destroy>>
        <<set $alvy5 to true>>
    <<endif>>
<<endif>>
    
===
title: Sam
tags: 
colorID: 4
position: 152,-711
---
<<if $biStage is 5>>
    <<if visited("Sam") is false>>
    SAM: Hello there! Have you seen Jaime? They were supposed to meet me here this morning, but they never showed.
    MAISON: They've probably been taken by the cult.
    SAM: Taken? What are you talking about? What cult?
    MAISON: Well - 
    AVERY: Go and talk to Officer Eden at the station. They can fill you in.
    SAM: Oh, well, thank you, I guess?
    <<move Sam PoliceStationExt destroy>>
    <<else>>
        <<stop Sam>>
        SAM: I hope nothing's happened to Jaime.
        SAM: I guess I should go and talk to that police officer. 
        <<move Sam PoliceStationExt destroy>>
    <<endif>>
    <<set $samJaime to true>>
    <<set $samStation to false>>
<<endif>>

<<if $biStage is 6>>
    <<if $samStation is false>>
    SAM: I spoke to Officer Eden like you said. Apparently people have been going missing all over the island.
    SAM: Why doesn't everyone know about this? I suppose with the election coming up, people have been distracted.
    SAM: Officer Eden said that we couldn't know for sure Jaime is missing, but I know they are. I...I don't know what to do.
    CHARLIE: We're looking for someone who's missing too. And we're going to find them. I promise.
    SAM: Well, thank you. I should...go home, I suppose. Good luck.
    <<set $samStation to true>>
    <<move Sam SamsHouseExt destroy>>
    <<else>>
        <<stop Sam>>
        SAM: Thank you for your help, but I think I'll just go home.
        <<move Sam SamsHouseExt destroy>>
    <<endif>>
<<endif>>
===
title: BA
tags: 
colorID: 4
position: 372,-489
---
<<if visited("BA") is false>>
    BAILEY: What Luca said about Tam was totally uncalled for!
    ASH: It was the truth and you know it. Luca would never say something like that if it wasn't - Oh, hi there.
    BAILEY: Can we help you?
    CHARLIE: I'm trying to find my sibling, Kell. Do you know them?
    BAILEY: I've seen them around, but I don't think we've ever had a conversation.
    ASH: Sorry, kid. But Bailey, seriously, Tam's only in it for the power.
    <<move Ash Apartments destroy>>
    <<move Bailey Apartments destroy>>
<<else>>
    BAILEY: We can't help you, sorry.
<<endif>>
===
title: Dee
tags: 
colorID: 4
position: 370,-705
---
<<if $biStage is 2>>
    DEE: Can this wait until after the debate?
<<endif>>

<<if $biStage is 3>>
<<if visited("Dee") is false>>
CHARLIE: Do you know Kell?
DEE: Yes, I know them. Why do you ask?
CHARLIE: I'm trying to find them. 
DEE: I haven't seen them since the mayor died a few days ago.
CHARLIE: I think they've gone missing.
DEE: Missing? You haven't been talking to Maison, have you?
DEE: That troublemaker was kicking up a fuss the other day, claiming that people were going missing.
<<if visited("TownHallExit") is true>>
    <<show Tam true>>
    <<move Tam BankExt destroy>>
<<endif>>
DEE: No one's missing. I'm sure Kell's just fine.
AVERY: Where can we find Maison?
DEE: They live in the cemetery. How they can stand to sleep in such a place, I'll never understand.
<<move Dee AlvysHouseExt destroy>>
AVERY: Sounds like this Maison might have an idea about what's going on.
CHARLIE: Let's go find them.
<<else>>
    DEE: I'm sorry, I can't talk right now.
<<endif>>
<<endif>>

<<if $biStage is 5 or $biStage is 6>>
    [[Dee&Riley]]
<<endif>>
===
title: Station
tags: 
colorID: 1
position: 814,-906
---
<<if $biStage is 4>>
    MAISON: Now you'll see how blind these dancing monkeys really are.
    <<Change Scene 9>>
<<endif>>

<<if $biStage is 5>>
    MAISON: We don't have enough to show Eden yet.
<<endif>>

<<if $biStage is 6>>
    MAISON: Now that we have proof, Officer Eden HAS to listen to us.
    <<Change Scene 9>>
<<endif>>

<<if $biStage is 9>>
    CHARLIE: I hope Officer Eden's in.
    <<Change Scene 9>>
<<endif>>

<<if $biStage is 1 or $biStage is 2 or $biStage is 3 or $biStage is 7 or $biStage is 8 or $biStage is 10>>
    AVERY: We don't need to go to the police station right now.
    //specific to each stage??
<<endif>>

===
title: Tam
tags: 
colorID: 6
position: 155,-278
---
<<if $biStage is 3>>
    <<show Tam true>>
    [[TamStage3]]
<<endif>>
<<if $biStage is 5>>
    [[OfficeStage5]]
<<endif>>
===
title: Luca
tags: 
colorID: 6
position: 370,-272
---
<<if $biStage is 3>>
    [[LucaStage3]]
<<endif>>


<<if $biStage is 10>>
    <<if visited("LucaStage10") is false>>
        <<stop Luca>>
        LUCA: Hello, Officer. And, er, friends. How can I help you?
        [[LucaStage10]]
    <<else>>
        LUCA: No! I won't be arrested! We must complete the ritual!
    <<endif>>
<<endif>>
===
title: TamStage3
tags: 
colorID: 6
position: -137,56
---
<<if visited("TamStage3") is false>>
    TAM: Who are you?
    CHARLIE: My name's Charlie. I'm looking for my sibling, Kell. 
    CHARLIE: They used to work here.
    TAM: I know Kell. Haven't seen them in a few days.
    TAM: You should go. I'm very busy.
<<else>>
    TAM: I told you to go.
<<endif>>

[[TamStage3A]]

===
title: LucaStage3
tags: 
colorID: 6
position: 366,-11
---
<<if visited("LucaStage3") is false>>
    LUCA: Hello there! What can I do for you?
    CHARLIE: Do you know Kell? They worked here with the last mayor.
    LUCA: Yes, I know Kell. Not very well, unfortunately. They mostly kept to themself.
    LUCA: You look a little like them, in fact. Are you related?
    CHARLIE: Kell's my sibling.
    LUCA: Of course. I can see the resemblance clear as day. 
    LUCA: It's wonderful to meet you - what was your name?
    CHARLIE: Charlie.
    LUCA: It's wonderful to meet you, Charlie. I never knew Kell had a sibling.
    CHARLIE: Do you know where they are? I'm trying to find them.
    LUCA: I'm sorry, but I'm afraid I can't help you. I haven't seen Kell for a few days.
    CHARLIE: Oh. Okay. Thanks anyway.
<<else>>
    LUCA: I'm sorry I can't help you find Kell. Is there anything else I can do for you?
    CHARLIE: Not right now.
<<endif>>

[[LucaStage3A]]
===
title: TownHall
tags: 
colorID: 0
position: -739,-437
---
<<if $biStage is 3 or $biStage is 5>>
    [[AveryTownHall]]
<<else>>
    <<if $biStage is 10>>
        <<if visited("LucaStage10") is false>>
            EDEN: When we get in there, let me -
            <<position Luca TownHallExt>>
            <<show Luca false>>
            EDEN: Luca. Just the person we were here to see.
            LUCA: Oh - Hello, Officer. And, uh, friends. I'm rather busy at the moment, just on my way to a meeting, perhaps we could -
            [[LucaStage10]]
        <<else>>
            AVERY: Luca's getting away! Let's go!
        <<endif>>
    <<else>>
        AVERY: We don't need to visit town hall right now.
    <<endif>>  
<<endif>>

  






===
title: AveryTownHall
tags: 
colorID: 0
position: -479,-438
---
<<if $biStage is 3>>
CHARLIE: Kell worked for the last mayor. Maybe someone in the town hall knows where they are.
<<if visited("AveryTownHall") is false>>
AVERY: I think I'll just...wait out here.
->Why?
    AVERY: Uhh...
    AVERY: Politicians, you know.
    CHARLIE: All right, I'll be back soon.
->Do what you want.
    AVERY: I will!
[[TownHall2]]
<<else>>
AVERY: I'll wait out here.
[[TownHall2]]
<<endif>>
<<endif>>

<<if $biStage is 5>>
    <<if $tam is false>>
        AVERY: I really don't think this is necessary. I bet Tam doesn't know anything.
        MAISON: Why are you so against talking to Tam? What are you hiding?
        AVERY: Nothing! 
        AVERY: Let's just go.
        [[TownHall2]]
    <<else>>
        MAISON: We've already got evidence from Tam's office.
        <<if $note is false>>
            AVERY: Let's see if we can find anything in Kell's house.
        <<else>>
            AVERY: Let's go and talk with Eden.
        <<endif>>
    <<endif>>
<<endif>>




===
title: TownHallExit
tags: 
colorID: 0
position: -476,-215
---
<<setFollow Maison false>>

<<if visited("Dee") is false>>
    <<show Dee true>>
<<endif>>

<<if visited("DR") is false>>
    <<show Devon true>>
    <<show Riley true>>
    <<lookAt Devon Riley>>
    <<lookAt Riley Devon>>
<<endif>>


<<if visited("TownHallExit") is false>>
AVERY: How'd it go?
CHARLIE: No one knows where Kell is. It's like they just disappeared.
AVERY: There must be someone who knows what happened to them.
<<else>>
AVERY: Anything?
CHARLIE: No.
    <<if visited("Dee") is true>>
        AVERY: Maybe we need to find this Maison.
    <<endif>>
<<endif>>

<<position Alvy BankExt>>
<<show Alvy false>>
<<move Alvy AlvysHouseExt destroy>>
<<position Bailey Apartments>>
<<show Bailey false>>
<<move Bailey SamsHouseExt destroy>>
===
title: KellsPlants
tags: 
colorID: 3
position: -502,-1136
---
<<if $biStage is 2>>
    CHARLIE: These plants look like they haven't been watered recently. 
<<else>>
    MAISON: Is that--that's belladonna.
    AVERY: Bella-what?
    MAISON: Belladonna. It's a poisonous plant.
    CHARLIE: Kell was always growing weird plants back when they lived on Blossom Island.
<<endif>>

===
title: KellsKitchen
tags: 
colorID: 3
position: -282,-1136
---
<<if $biStage is 2>>
    CHARLIE: There's no food here...
<<else>>
    MAISON: We're not here to eat, we're here to look for proof!
    MAISON: Although I could really go for a sandwich...
<<endif>>

===
title: KellsLab
tags: 
colorID: 3
position: -64,-1137
---
<<if $biStage is 2>>
    CHARLIE: What's all this?
    AVERY: Looks like some kind of lab equipment.
    CHARLIE: I don't understand any of these notes...Are they formulas?
<<else>>
    MAISON: Fascinating...
    CHARLIE: Did you find anything?
    MAISON: These notes are full of formulas for different elixirs and concoctions. It's all in shorthand though, I can't tell what they're for.
    AVERY: Did you find anything about the cult?
    MAISON: Oh. Well. No.
    MAISON: But these notes are really interesting. I could spend days deciphering them!
    CHARLIE: Let's keep looking.
<<endif>>

===
title: AveryBerry
tags: 
colorID: 0
position: -1351,-604
---
<<if $biStage is 1>>
//between docks and kell's house
AVERY: You want to visit Kell first, yeah? 
AVERY: If you've forgotten where their house is, maybe you should have a look at the map Redpeel gave us.
<<endif>>

<<if $biStage is 2>>
//from kell's house to debate
AVERY: Let's see what that noise is. It sounds like it's coming from the town square.
<<endif>>

<<if $biStage is 3>>
//after debate to finding maison
//includes optional conversations with tam and luca
    <<if visited("Dee") is false>>
        AVERY: Let's ask around a bit. Maybe someone's seen Kell.
    <<else>>
        AVERY: That orange said someone called Maison knew about people disappearing. 
        AVERY: They said Maison lived in the cemetery. It's probably on the map.
    <<endif>>
<<endif>>

<<if $biStage is 4>>
//between leaving maison's house and talking to eden
    AVERY: We need to talk to that police officer about what Maison knows.
    AVERY: The police station's probably in the centre of town somewhere. Let me see the map.
<<endif>>

<<if $biStage is 5>>
//after talking to eden, looking for evidence with both tam and kell
    <<if $note is false>>
        AVERY: We should search Kell's house. I bet we'll find something there to prove the cult took them.
    <<endif>>
    <<if $tam is false>>
        AVERY: I don't think we need to investigate Tam, but Maison's pretty insistent about it. 
        AVERY: Their office is in the town hall.
    <<endif>>
<<endif>>

<<if $biStage is 6>>
//begins after going to both tam and kell, lasts until speaking to eden then exiting station
    AVERY: We've found enough to prove the cult is real. We should take our evidence to Eden.
<<endif>>

<<if $biStage is 7>>
//looking for proof that cult is involved in disappearances (go straight to maison's place)
//no chance for interaction with party members
<<endif>>

<<if $biStage is 8>>
//after decoding note, going to cabin and exploring there
    AVERY: We need to go to the cult's hideout and find proof that they're taking people.
    AVERY: Wow. Cults, missing people...This is definitely not how I thought this trip to Berry Island would go.
<<endif>>

<<if $biStage is 9>>
//after seeing luca and devon in cult hideout
    AVERY: We have to go and show Eden these photos. We'll catch Luca and the other cultists, and then they can tell us where Kell is.
<<endif>>

<<if $biStage is 10>>
//after telling eden about luca, includes chasing luca and heading to the docks
    <<if visited("LucaStage10") is false>>
        <<if visited("DoorNote") is false>>
            AVERY: Luca's probably still at the cabin. We can catch them if we hurry.
        <<else>>
            AVERY: We need to find Luca. They might be in their office.
        <<endif>>
    <<else>>
            AVERY: Quick, after Luca! It looks like they're heading for the docks behind the cabin!
    <<endif>>
<<endif>>
===
title: OfficeStage5
tags: 
colorID: 6
position: 128,110
---
CHARLIE: Tam's not here.
MAISON: Quick, let's search their office before they get back.
===
title: TamsDesk
tags: 
colorID: 6
position: -407,26
---
CHARLIE: Look, I found a note.
MAISON: What does it say, what does it say?
CHARLIE: "Meeting tonight at dusk. You know where. Don't let anyone see you."
MAISON: PROOF! 
MAISON: I told you Tam was part of the cult!
AVERY: We don't know that for sure. That note could mean anything.
MAISON: No it couldn't! 
->Avery's right.
    CHARLIE: It's suspicious, but it doesn't say anything about a cult.
    AVERY: Told you.
->We need more evidence.
    CHARLIE: Let's keep looking before we jump to any conclusions.
->Maison's right.
    CHARLIE: This proves that Tam's involved with the cult.
    MAISON: I knew you were a smart one, Charlie.
<<show Tam true>>
<<position Tam Entry>>
[[TamStage5]]
===
title: Stage5
tags: 
colorID: 0
position: -1409,181
---
<<if $stage5 is "tam">>
    <<set $tam to true>>
    [[TamExit]]
<<endif>>

<<if $stage5 is "station">>
    [[StationExit]]
<<endif>>

<<if $stage5 is "KellsHouse">>
    <<set $note to true>>
    [[KellsHouseExit]]
<<endif>>


    
===
title: KellsNote
tags: 
colorID: 3
position: -499,-698
---
CHARLIE: I think there's something under the rug.
AVERY: It looks like some kind of note.
<<Take Note>>
CHARLIE: It's written in code. I can't read it.
MAISON: Let me see. 
MAISON: It'll take me a while to decode it. But here, look, it's the cult symbol. One of them must have dropped it here.
MAISON: It's proof that the cult exists! 
CHARLIE: And that they took Kell.
<<set $stage5 to "KellsHouse">>
<<set $note to true>>
===
title: Stage6
tags: 
colorID: 1
position: 825,-38
---
MAISON: I told you involving the police was a waste of time.
CHARLIE: We just need to find something connecting the cult to the disappearances.
AVERY: Where do we even start looking?
MAISON: Maybe there's something in the note we found at Kell's. Let me see it.
//MAISON: Let's go back to my place. I can decode it.
//AVERY: Back to the creepy graveyard, then...
<<set $biStage to 7>>
<<position Ash Apartments>>
<<show Ash false>>
<<move Ash PoliceStationExt destroy>>
//<<Change Scene 8>>

//Change here: Maison solves the note now because it's really not necessary to go back to their place
//if another solving method is introduced eg puzzle of some kind, then change it back.
MAISON: Hmm...It looks like they might be using a simple substitution cipher. Amateurs.
MAISON: A little frequency analysis, some logical deduction...
MAISON: ...
MAISON: Solved it. It says the cult's hideout is in a cabin on the northeast side of the island.
CHARLIE: Let's go check it out.
<<set $biStage to 8>>

<<position Alvy Cinema>>
<<show Alvy false>>
<<move Alvy BankExt destroy>>

===
title: BERRYNOTES
tags: 
colorID: 0
position: -1331,-1062
---
Berry Island: 6
Kell's House: 7
Maison's House: 8
Station: 9
Luca's Office: 10
Tam's Office: 11
Cult hideout cabin: 12

Stage 1:
Stage 2:
Stage 3:
Stage 4: after maison's conspiracy talk, talking to eden (set in maisonshouseint)
Stage 5: (set in stationint) finding evidence 
Stage 6: after visiting both Tam and Kell's house to find evidence, lasts until conversation after exiting station
Stage 7: after bringing evidence of cult to Eden and setting out to look for proof that cult is involved in disappearances (set in "Stage6")
Stage 8: after (Maison) has solved the puzzle (set in MaisonsHouseInt)
Stage 9: after seeing Luca and Devon in cult hidout. (set in Hideout)
Stage 10: after telling Eden about Luca (set in PoliceStation3)
===
title: MaisonsHouse
tags: 
colorID: 2
position: 596,-477
---
<<if $biStage is 3>>
//concern that tone is lost here. difficult to convey without inflection?   
    //CHARLIE: [knocks] Hello?
    CHARLIE: The door's locked.
    DOOR: VOICEPRINT NOT RECOGNISED. ENTRY DENIED.
    AVERY: Voiceprint?
    DOOR: VOICEPRINT NOT RECOGNISED. ENTRY DENIED.
    AVERY: Is that a keypad?
    CHARLIE: And a thumbprint scanner. And a retinal scanner.
    AVERY: Seems excessive.
    CHARLIE: Hello? Is anyone there? We're looking for Maison.
    DOOR: MAISON'S NOT HERE.
    CHARLIE: ...
    CHARLIE: We'll just wait until they get back, then.
    DOOR: THEY WON'T BE BACK FOR A LONG TIME. A REALLY, REALLY LONG TIME.
    AVERY: That's okay. We're not in a hurry.
    DOOR: ...
    DOOR: GO AWAY.
    CHARLIE: We're not leaving until we've talked to you.
    DOOR: ME? WHY WOULD YOU WANT TO TALK TO ME? I'M A DOOR.
    AVERY: Riiiiiight. Sure you are.
    <<if visited("Dee") is true>>
        CHARLIE: We're trying to find someone. We heard that you knew something about people going missing.
    <<else>>
        CHARLIE: We're trying to find someone. Do you know Kell? We think they might be missing.
    <<endif>>
    DOOR: OH, YOU'RE HERE ABOUT THE MISSING PEOPLE? WHY DIDN'T YOU SAY SO? COME RIGHT IN.
    <<Change Scene 8>>
<<else>>
    <<if $biStage is 1 or $biStage is 2>>
        AVERY: Who would live in a cemetery?
    <<else>>
        AVERY: Is all this security necessary?
        CHARLIE: I don't think I've ever seen so many locks.
        MAISON: You'd have this much security too, if you knew what was really out there.
    <<endif>>
<<endif>>
===
title: CabinFrontDoor
tags: 
colorID: 0
position: 1030,-907
---
<<if $biStage is 8>>
    <<if visited("CabinFrontDoor") is false>>
        CHARLIE: It's locked.
        AVERY: Out of the way, Charlie. I'll kick it down.
        MAISON: Wait, we can't just charge through the front door.
        AVERY: Why not?
        MAISON: Because it will draw the attention of every cultist inside. I don't know about you, but I don't want to get caught and eaten.
        MAISON: Or maybe you do want us to get caught. Because you're working with the cult.
        AVERY: Not this again.
        AVERY: We can deal with whoever's inside.
        MAISON: Or we could do the smart thing and find a different entrance.
        CHARLIE: We should probably try not to draw too much attention.
        CHARLIE: Let's try to find another way in.
    <<else>>    //if already had discussion
        MAISON: We need to find another way in.
        CHARLIE: Let's keep looking.
    <<endif>>
<<endif>>

<<if $biStage is 9>>
    AVERY: We need to show Eden what we found. 
    MAISON: And we should go before the cultists find us.
<<endif>>

<<if $biStage is 10>>
    <<if visited("LucaStage10") is true>>
        MAISON: I think they went around the back to the docks. Quick, they're getting away!
    <<else>>
        [[DoorNote]]
    <<endif>>
<<endif>>

<<if $biStage is 1 or $biStage is 2 or $biStage is 3 or $biStage is 4 or $biStage is 5 or $biStage is 6 or $biStage is 7>>
    CHARLIE: We probably shouldn't just barge into a stranger's house. That seems rude.
<<endif>>
===
title: CabinSideDoor
tags: 
colorID: 0
position: 1032,-683
---
<<if $biStage is 8>>
    <<if visited("CabinFrontDoor") is true>>
        MAISON: A side door. Much more subtle than barging through the front door.
        AVERY: Whatever.
        CHARLIE: It's locked too.
    <<else>>
        CHARLIE: This door's locked.
    <<endif>>
    MAISON: Good thing I know how to pick locks.
    AVERY: Of course you do.
    MAISON: Time to prove what the cult's up to once and for all.
    <<Change Scene 12>>
<<endif>>

<<if $biStage is 9>>
    AVERY: We've got to take this evidence to Eden.
<<endif>>

<<if $biStage is 10>>
    <<if visited("LucaStage10") is true>>
        AVERY: There's Luca, look! At the dock!
    <<else>>
        [[DoorNote]]
    <<endif>>
<<endif>>

<<if $biStage is 1 or $biStage is 2 or $biStage is 3 or $biStage is 4 or $biStage is 5 or $biStage is 6 or $biStage is 7>>
    CHARLIE: We probably shouldn't just barge into a stranger's house. That seems rude.
<<endif>>

===
title: CabinInt
tags: 
colorID: 0
position: 1034,-469
---
CHARLIE: Let's see if we can find anything that links the cult to the missing people.

===
title: CabinDesk
tags: 
colorID: 0
position: 1037,-247
---
AVERY: Anything interesting?
CHARLIE: I think these are take-out menus...
===
title: CabinDoor1
tags: 
colorID: 0
position: 1047,-36
---
CHARLIE: It's locked.
===
title: Crates
tags: 
colorID: 0
position: 1051,185
---
//CHARLIE: [smashes the crates]
//MAISON: Hopefully no one heard that.

CHARLIE: These crates are filled with...hats. Just lots of hats.
===
title: CabinDoor2
tags: 
colorID: 0
position: 1251,-909
---
//CHARLIE: I think I hear something on the other side...
//AVERY: Let's go, then.
//Maison: Quietly.
//<<open CageRoomDoor>>
===
title: Stage10
tags: 
colorID: 1
position: 840,395
---

//<<set $townhall false>>
//<<set $searchedCabin to false>>
EDEN: Where did you see Luca last?
CHARLIE: The cabin.
EDEN: Take me there, quickly.
<<show Luca true>>
<<position Luca CabinExt>>
<<move Luca PointA no>>
<<setFollow Eden true>>
<<set $luca10 to false>>

===
title: RedpeelBerry
tags: 
colorID: 0
position: -716,-1133
---
CAPTAIN REDPEEL: You lookin' to get off the island, kid?
CHARLIE: Not right now.
CAPTAIN REDPEEL: Get on, then.
===
title: TownHall2
tags: 
colorID: 0
position: -227,-433
---
<<if $biStage is 3>>
->[Tam's Office]
    <<Change Scene 11>>
->[Luca's Office]
    <<Change Scene 10>>
->[Come back later]
<<endif>>

<<if $biStage is 5>>
->[Tam's Office]
    <<Change Scene 11>>
->[Come back later]
<<endif>>
===
title: TamStage3A
tags: 
colorID: 6
position: -175,-198
---
->[Luca's Office]
    <<Change Scene 10>>
->[Leave]
    <<Change Scene 6>>
===
title: LucaStage3A
tags: 
colorID: 6
position: 603,-202
---

->[Tam's Office]
    <<Change Scene 11>>
->[Leave]
    <<Change Scene 6>>
===
title: MaisonBerry
tags: 
colorID: 0
position: -1565,-601
---
//MAISON: You know, Charlie, once this is all done there's a few things I could use your help with...

<<if $biStage is 4>>
//between leaving maison's house and talking to eden
    MAISON: Bringing the cops into this isn't going to help, you know. You'll see, when we talk to that officer.
    MAISON: Useless idiots, the lot of them. Haven't got more than three brain cells between them.
    //fools...
<<endif>>

<<if $biStage is 5>>
//after talking to eden, looking for evidence with both tam and kell
    <<if $tam is false>>
        MAISON: Tam's definitely involved with the cult. We need to interrogate them.
        MAISON: They're probably in their office in the town hall.
    <<endif>>
    <<if $note is false>>
        MAISON: We're not going to find anything in Kell's house.
        MAISON: ...But maybe we should look there anyway, just in case.
    <<endif>>
<<endif>>

<<if $biStage is 6>>
//begins after going to both tam and kell, lasts until speaking to eden then exiting station
    MAISON: We need to show our proof to that cop! Now they'll have to believe me about the cult!
    MAISON: To the police station!
<<endif>>

<<if $biStage is 7>>
//looking for proof that cult is involved in disappearances (go straight to maison's place)
    //no chance for interaction with party members
<<endif>>

<<if $biStage is 8>>
//after decoding note, going to cabin and exploring there
    MAISON: The note said the cult had a hideout in a cabin on the northeast side of the island.
    MAISON: We need to go there and get proof that they're taking and eating people. It's going to be gruesome, so you'd better prepare yourself.
<<endif>>

<<if $biStage is 9>>
//after seeing luca and devon in cult hideout
    MAISON: I knew that someone in town hall was involved with the cult, I knew it! 
    MAISON: We have to go and tell Eden about this. I can't wait to see their face when they admit I was right.
<<endif>>

<<if $biStage is 10>>
//after telling eden about luca, includes chasing luca and heading to the docks
    <<if visited("LucaStage10") is false>>
        <<if visited("DoorNote") is false>>
            MAISON: I bet Luca's still in the cabin. If we hurry we can catch them there.
        <<else>>
            MAISON: We can't let Luca get away with this! To their office, quick!
        <<endif>>
    <<else>>
        MAISON: We need to catch Luca before they get away! They're heading for the dock behind the cabin!
    <<endif>> 
<<endif>>

===
title: KellSatchel
tags: 
colorID: 3
position: -66,-697
---
<<if $biStage is 2>>
    CHARLIE: Kell's satchel is here. Why wouldn't they take it with them when they left the house?
    AVERY: Maybe they're just running a quick errand?
<<else>>
    CHARLIE: Kell wouldn't have left home without their satchel.
    MAISON: More evidence that they were taken against their will. 
<<endif>>
===
title: AveryKH
tags: 
colorID: 3
position: -503,-1345
---
<<if $biStage is 2>>
    AVERY: Kell's not here. Maybe we should see if they're somewhere in town.
<<else>>
    <<if $note is false>>
        AVERY: There must be proof that Kell was kidnapped SOMEWHERE in here.
    <<else>>
        AVERY: We've got proof now. Let's take it to Eden.
        <<if $tam is false>>
            MAISON: We still need to interrogate Tam.
            AVERY: Not this again...
        <<endif>>
    <<endif>>
<<endif>>
===
title: AveryTam
tags: 
colorID: 0
position: -1561,-376
---
AVERY: This is pointless.
===
title: MaisonTam
tags: 
colorID: 0
position: -1342,-376
---
MAISON: We should see if there's anything on Tam's desk.
===
title: MaisonKH
tags: 
colorID: 3
position: -292,-1342
---
<<if $note is false>>
    MAISON: I don't think we're going to find anything here.
    MAISON: But if it makes you feel better, search away.
<<else>>
    MAISON: Maybe it was worth searching this place.
    AVERY: Told you.
    <<if $tam is false>>
        MAISON: But we should still interrogate Tam.
    <<endif>>
<<endif>>
===
title: AveryHideout
tags: 
colorID: 0
position: 1027,-1112
---
AVERY: We should look around and see if we can find any proof that the cult's kidnapping people.
===
title: MaisonHideout
tags: 
colorID: 0
position: 1251,-1118
---
MAISON: You know, I thought there would be more dead bodies in here.
MAISON: They must keep them in a fridge somewhere.
===
title: EdenBerry
tags: 
colorID: 0
position: -1129,-600
---
<<if $biStage is 3>>
    EDEN: You should ask around and see if anyone knows where Kell is. 
    EDEN: Come and find me in the police station if you learn anything.
    <<move Eden PoliceStationExt destroy>>
<<endif>>

<<if $biStage is 10>>
    <<if visited("LucaStage10") is false>>
        <<if visited("DoorNote") is false>>
            EDEN: Take me to where you last saw Luca. 
        <<else>>
            EDEN: We need to stop Luca before they do any more damage. Let's see if they're in their office.
        <<endif>>
    <<else>>
        EDEN: After Luca, quick!
    <<endif>>
<<endif>>
===
title: Bailey
tags: 
colorID: 0
position: 365,-1148
---
<<if $biStage is 2>>
    BAILEY: Could we speak later? I want to hear what they have to say.
<<endif>>

<<if $biStage is 3>>
    <<if visited("TownHallExit") is false>>
        [[BA]]
    <<else>>
        <<stop Bailey>>
        <<if visited("BA") is true>>
            BAILEY: Are you still looking for Kell? Perhaps you should try talking to Maison.
            BAILEY: They're a little mad, but they know a lot about what goes on around here.
            BAILEY: Their house is in the cemetery. I hope they have some answers for you.
        <<else>>
            BAILEY: Can I help you?
            CHARLIE: I'm trying to find my sibling, Kell. Do you know them?
            BAILEY: I've seen them around, but I don't think we've ever had a conversation. Sorry.
        <<endif>>
        <<move Bailey SamsHouseExt destroy>>
    <<endif>>
<<endif>>

<<if $biStage is 5 or $biStage is 6 or $biStage is 8>>
    BAILEY: I'm sorry, I'm rather busy at the moment.
<<endif>>

<<if $biStage is 9>>
    BAILEY: I'm waiting for Alvy to get here so we can go and see "Citrus 2: Partners in Lime". 
    BAILEY: Have you seen it? Wait, don't tell me, I don't want to be spoiled.
<<endif>>
===
title: Ash
tags: 
colorID: 0
position: 356,-1362
---
<<if $biStage is 2>>
    ASH: Not now, kid, this is getting interesting.
<<endif>>

<<if $biStage is 3>>
    [[BA]]
<<endif>>

<<if $biStage is 5 or $biStage is 6 or $biStage is 8>>
    ASH: Sorry kid, no time to talk.
<<endif>>

<<if $biStage is 9>>
    ASH: You look like you're in a hurry.
    CHARLIE: Sorry, no time to talk.
<<endif>>
===
title: Dee&Riley
tags: 
colorID: 0
position: 147,-1140
---
<<if visited("Dee&Riley") is false>>    
    DEE: None of this would be of any concern if the previous mayor hadn't died so abruptly -
    DEE: Oh, hello there. 
    RILEY: Did you need something?
    CHARLIE: Have either of you heard anything about a cult?
    DEE: I see Maison's gotten to you. 
    MAISON: The cult is real. You're so comfortable in your bubble of ignorance -
    AVERY: And that's our cue to leave.
<<else>>
    DEE: You really shouldn't listen to Maison's conspiracy talk. It does no one any good.
<<endif>>
===
title: Riley
tags: 
colorID: 0
position: 137,-1365
---
<<if $biStage is 2>>
    RILEY: Shh, I'm trying to listen.
<<endif>>

<<if $biStage is 3 or $biStage is 4>>
    [[DR]]
<<endif>>

<<if $biStage is 5 or $biStage is 6>>
    [[Dee&Riley]]
<<endif>>
===
title: DoorNote
tags: 
colorID: 0
position: 1268,-227
---
<<if visited("DoorNote") is false>>
CHARLIE: There's a note on the door. It looks like it's written in code.
MAISON: Let me see. Hmm.
MAISON: ...
MAISON: It says "Hideout may be compromised. Lay low and wait for further instruction."
AVERY: Now what do we do? Where would Luca go?
CHARLIE: Maybe we should try their office.
EDEN: Good idea, Charlie. Let's go.
<<else>>
    EDEN: The note says the cult has abandoned the cabin. We need to go to Luca's office in town hall.
<<endif>>
===
title: Devon
tags: 
colorID: 0
position: 586,-1368
---
<<if $biStage is 2>>
    DEVON: Not now! I'm listening!
<<endif>>

<<if $biStage is 3 or $biStage is 4>>
    [[DR]]
<<endif>>

<<if $biStage is 5>>
    [[Devon5]]
<<endif>>
===
title: Devon5
tags: 
colorID: 0
position: 822,-1369
---
<<if visited("Devon5") is false>>
    <<stop Devon>>
    DEVON: I have a million things to do. This better be important.
    MAISON: You're helping run Luca's mayoral campaign. What have you got to say about the conspiracy to cover up the last mayor's murder?
    DEVON: ...
    DEVON: Bye.
    <<move Devon CabinExt destroy>>
<<else>>
    DEVON: I don't have time for your crazy conspiracies.
<<endif>>
===
title: PreDebate
tags: 
colorID: 4
position: 122,-1579
---
CHARLIE: It looks like there's some kind of town meeting just ahead.
AVERY: Let's see what it's about.
===
title: MeetingHint
tags: 
colorID: 0
position: 352,-1568
---
AVERY: I think we're going in the wrong direction. We need to head to the town square.
===
