title: JackTask1
tags: 
colorID: 2
position: 1275,371
---
JACK: Oh, hi Charlie. Um. Do you—do you like poetry?
->Oh yeah, I love poetry!
    JACK: Awesome! What do you think of this?
->Uhhh...what?
    JACK: Poetry—you know, expressing inner thoughts and emotions through the medium of—Never mind, just listen and tell me what you think.
->I despise it 
    Charlie: With every fibre of my being.
    JACK: Oh...well, maybe you can tell me if you hate this a lot or just a little bit.

JACK: There once was a fruit called Finn
Their heart Jack wanted to win
So Jack wrote this poem
Hoping it would show 'em
That they are the best lemon

->That was, uh, great.
    JACK: You really think so? You’re the best, Charlie. I’m going to give it to Finn.
->It needs a little work.
    JACK: True art is never perfect, Charlie. It'll always need a little work.
->My ears are bleeding.
    JACK: ...oh. Okay. Um. Thanks for your honesty, I guess. Was it really that bad?
    JACK: No, you know what? I don’t care! I poured my heart and soul into this poem and I am not going to throw it away!
    JACK: I’m going to give it to Finn! Even if it’s bad, the sentiments are real!

JACK: Ugh, now I’m nervous. Do you think Finn will like it? They’ll like it, won’t they? 
JACK: Hey...Charlie, will you give it to Finn for me? They’re working in the market today, so you don’t have to go far. 
JACK: Please? I'll pay you!
->Sure.
    JACK: Great! Awesome! Thanks Charlie! Don't forget to bring back their answer!
    <<set $jackaccept to true>>
    <<set $jackcomplete to false>>
->Not right now.
    JACK: Oh. Okay...
    <<set $jackaccept to false>>


===
title: RedpeelStart
tags: 
colorID: 1
position: 2127,108
---
<<if visited("RedpeelStart") is false>>
CAPTAIN REDPEEL: How’s it goin’ kid? What brings you to the docks this fine mornin’?
CHARLIE: Can you take me to Berry Island?
CAPTAIN REDPEEL: Arr, I can take ye. For fifty gold doubloons.
CHARLIE: Fifty...doubloons?
CAPTAIN REDPEEL: Dollars. It’s fifty dollars.
CHARLIE: I don't have that much.
CAPTAIN REDPEEL: Then get goin', kid. I ain't runnin' a charity. Pay or stay. 
CAPTAIN REDPEEL: If you want to earn some money, try talkin’ to some folks. Maybe they’ll give you a few bucks if you help ‘em out.
CHARLIE: I should see if I have anything lying around at home first.

<<else>>
CAPTAIN REDPEEL: You got the money yet, kid?
    <<if $money < 50>>  //doesn't have the money
        CHARLIE: Not yet.
        CAPTAIN REDPEEL: Get on, then. See if anyone around the village needs some help.
    <<else>>    //has the money and is with avery
    //position avery with redpeel?
        CHARLIE: Yep, we both do.
        CAPTAIN REDPEEL: Arr, excellent.
        CAPTAIN REDPEEL: We be sailin’ soon, so you just hop on board now.
        <<set $biStage to 1>>
        <<Change Scene 6>>
    <<endif>>
<<endif>>
===
title: FinnTask1A
tags: 
colorID: 3
position: 1814,392
---
CHARLIE: Jack wanted me to give you this.
FINN: [reads poem] 
FINN: Oh boy. This is...
CHARLIE: It's terrible.
FINN: Well, yeah, but it's also kind of sweet. 
CHARLIE: Jack wanted me to bring back your answer.
FINN:...I don't know what to say. I've never thought about Jack like that before. 
FINN: What do you think, Charlie? 
->Give Jack a chance.
    <<set $finnyes to true>>
    CHARLIE: You guys could be good together.
    FINN: Yeah. Yeah, you're right. Just-hang on a sec, okay? 
    FINN: [writes something on a piece of paper]
    FINN: Give this to Jack. Thanks, Charlie.
->I'd stay away.
    <<set $finnyes to false>>   //make no clearer?
    CHARLIE: You're just going to get more terrible poetry.
	FINN: Hmm, I guess so. 
    FINN: You should go and tell Jack. Be nice to them, okay?

===
title: FinnTask1B
tags: 
colorID: 3
position: 2063,392
---
CHARLIE: Jack gave me a poem to give to you, but I won't subject you to it. 
FINN: Ha ha...Is it really that bad?
[[Here, see for yourself. (give poem)|FinnTask1A]]
[[It's really that bad.|FinnTaskC]]
===
title: FinnTaskC
tags: 
colorID: 3
position: 2068,647
---
FINN: Poor Jack. Points for trying, I guess. 
CHARLIE: They wanted me to bring back your answer.
//option to influence finn here?
FINN: Will you tell them I'm not interested?
FINN: Thanks, Charlie, you're the best.
<<set $finnyes to false>>
===
title: NancyTask1
tags: 
colorID: 2
position: 1531,369
---
NANCY: Oh, Charlie, dear! Will you help me? There’s a swarm of fruitbats in my attic and they’re making an awful mess.
->Sure.
    <<set $nancyaccept to true>>
    NANCY: Oh, thank you, Charlie!
->Not right now.
    <<set $nancyaccept to false>>
    NANCY: Please come back soon, Charlie. I need your help.
NANCY: I've been having so much trouble here alone, since my dear Harold died. Harold was my spouse, you know.
CHARLIE: I know. I've lived on this island my whole life. 
NANCY: Harold died a few years ago, and I miss them so much. They used to be the one who would deal with things like this, you know.
NANCY: Dear Harold. Did I ever tell you about the time we went on holiday to Orchard Island?
CHARLIE: You might have mentioned it once or twice.
NANCY: It was so wonderful. Orchard Island is beautiful, Charlie, you should visit sometime. Harold loved it there. 
NANCY: We journeyed all over that island, and it was the best time of my life. 
NANCY: The sun, the people, the food...and, of course, Harold. 
<<if $nancyaccept is true>>
    CHARLIE: About those bats...
    NANCY: Ah, yes, the bats. Do you think you can handle them, dear? Maybe I should ask someone else. You're just a little thing. 
<<else>>
    CHARLIE: Sounds great...
<<endif>>
NANCY: You live alone, don't you, since Kell moved to Berry Island. Are you eating enough? Are you looking after yourself? It's quiet, living alone. 
NANCY: Are you lonely? You should come and visit more often, dear. We can keep each other company. I can tell you more about Harold.
<<if $nancyaccept is true>>
    CHARLIE: I really should go and deal with those bats now.
    NANCY: Of course, dear. You're so helpful, Charlie. Just like Harold. Oh, I miss them. 
<<else>>
    CHARLIE: I think I know enough about Harold.
<<endif>>
NANCY: Did I ever tell you about that time we went fishing off the coast of Berry Island?
NANCY: I'm not much of a one for fishing, to tell the truth, but Harold loved it so. 
NANCY: They caught all sorts of fish on that trip. They were very good at fishing, you know. The best on Blossom Island! Do you like fishing, Charlie?
<<if $nancyaccept is true>>
    CHARLIE: No, but I like clearing bats out of attics.
    NANCY: Oh, excellent! There’s a swarm of fruitbats in my attic and they’re making an awful mess.
    CHARLIE: I know. You told me.
    NANCY: Did I? I've been having trouble remembering things since dear Harold died.   
    NANCY: Harold was my spouse, you know.
    CHARLIE: ...I'm just gonna go deal with those bats now.
    <<set $batscomplete to true>>
    <<Change Scene 3>>
<<else>>
    CHARLIE: I do like fishing. In fact, I'm going to go fishing right now. Bye.
    NANCY: Oh...goodbye.
    NANCY: Come back when you're ready to get rid of those bats for me!
<<endif>>


    
    
===
title: AveryStart
tags: 
colorID: 1
position: 2905,108
---
<<if visited("AveryStart") is false>>
    <<if $foundDog is false>>
        AVERY: Hey, you heading into the forest? Wanna help me out?
        AVERY: I’m looking for Kim’s dog but there’s a bunch of wolves in there and I can’t handle them on my own.
        CHARLIE: I’m looking for the dog too. You think the two of us can take those wolves?
        AVERY: Depends. How good are you with that bat?
        ->I guess we’ll find out.
            AVERY: Love the confidence, there.
        ->You should ask Nancy.
            CHARLIE: I just took out an attic full of fruitbats for them.
            AVERY: Well, I guess that’s something. 
        ->I'm amazing.
            AVERY: That’s some big talk, Charlie. Hope you can back it up! 
        AVERY: Come on, let’s go get that dog!
        ->Yeah!
            <<set $averyaccept to true>>
            <<set $forest to true>>
            <<Change Scene 5>>
        ->Just give me a minute.
            <<set $averyaccept to false>>
    <<else>>
        <<if visited ("Kim2") is false>>
            AVERY: We should bring Spot back to Kim.
        <<else>>
            AVERY: To the docks!
        <<endif>>
    <<endif>>
<<else>>
    <<if $averyaccept is false>>
        AVERY: Ready to head into the forest?
        ->Let's go.
            <<set $averyaccept to true>>
            <<set $forest to true>>
            <<Change Scene 5>>
        ->Not yet.
            AVERY: We should probably go soon, before that poor little dog gets eaten.
    <<else>>
        <<if visited ("Kim2") is false>>
            AVERY: We should bring Spot back to Kim.
        <<else>>
            AVERY: To the docks!
        <<endif>>
    <<endif>>
<<endif>>
    


===
title: NancyStart
tags: 
colorID: 1
position: 1529,104
---
<<if visited ("RedpeelStart") is false>>
NANCY: Hello there, Charlie. Isn't it a lovely day? 
NANCY: The sun is a little warm, though. Hmm, I might have to fetch my parasol from the attic.

<<else>>
    <<if $nancyaccept is false>>
        <<if visited ("NancyTask1") is false>>
            [[NancyTask1]]
        <<else>>
            NANCY: Will you get rid of the fruitbats in my attic now?
            NANCY: Harold always used to do that kind of thing, you know, before they died.
            ->I'll do it.
                NANCY: Oh, wonderful! Thank you so much, dear. 
                NANCY: You be careful, now. Those bats can be vicious.
                <<set $nancyaccept to true>>
                <<Change Scene 3>>
            ->Maybe later.
                NANCY: Please come back soon, Charlie.
        <<endif>>
    <<else>>
        <<if visited ("Nancy2") is false>>
            [[Nancy2]]
        <<else>>
            NANCY: Would you like to hear more about Harold?
            CHARLIE: Maybe another time.
        <<endif>>
    <<endif>>
<<endif>>
===
title: JackStart
tags: 
colorID: 1
position: 1273,107
---
<<if visited ("RedpeelStart") is false>>
JACK: Can't talk now, I'm working on something really important!

<<else>>
    <<if visited ("JackTask1") is false>>  
        [[JackTask1]]   //visit jack and learn about the task
    <<else>>
        <<if $jackaccept is true>>
            <<if $jackcomplete is false>>   //accepted and not complete
                [[JackTask2]]   
            <<else>>    //accepted and is complete
                [[JackCompleted]]
            <<endif>>
        <<else>>    
           [[JackTask1A]]  //if you haven't accepted the task
        <<endif>>
    <<endif>>
<<endif>>
===
title: FinnStart
tags: 
colorID: 1
position: 1829,107
---
<<if visited ("RedpeelStart") is false>>
FINN: Hi Charlie! What can I do for you?
->I want to go to Berry Island.
    FINN: Just go and talk to Captain Redpeel at the docks, they'll get you there.
->Just wanted to say hi.
    FINN: Oh. Hi! 
<<else>>
    <<if visited ("JackTask1") is false>>   //if haven't visited jack
        FINN: Hey Charlie!
    <<else>>
        <<if visited ("FinnTask1A") is false and visited ("FinnTask1B") is false>>
        //if they haven't talked to finn about the poem
        FINN: Hey Charlie!
        [[(Give poem)|FinnTask1A]]
        [[(Don't give poem)|FinnTask1B]]
        <<else>>
        //if they've talked to finn about the poem and got their response and
            <<if $jackcomplete is true>> //have brought it to jack
                [[FinnCompleted]]
            <<else>> //haven't brought it to jack
                [[FinnTask2]]
            <<endif>>
        <<endif>>
    <<endif>>
<<endif>>
===
title: JackTask2
tags: 
colorID: 2
position: 1144,638
---
JACK: Did you speak to Finn? What did they say?
<<if visited("FinnTask1A") is false and visited("FinnTask1B") is false>>
CHARLIE: I haven't spoken to them yet.
JACK: Please do it soon, I'm going to puke if I have to wait much longer.
<<else>>
    <<if $finnyes is true>>
        CHARLIE: Here, see for yourself. (give Finn's poem)
        JACK: Finn likes me? Finn likes me! This is the best day of my life! 
        JACK: I'm going to go and talk to them, bye Charlie, thanks!
        JACK: Oh, and here's your money!
        <<move Jack JackDestination>>
    <<else>>
        CHARLIE: They're not interested. Sorry, Jack.
        JACK: Oh...okay. I...I guess I'll...just...Here's your money. Bye Charlie.
    <<endif>>
    <<set $jackcomplete to true>>
    <<set $money to $money + 10>>
    <<if $jackcomplete is true and $nancycomplete is true and $houseScene is true>>
        <<activate Kim>>
        <<move Kim Charlie>>
    <<endif>>


<<endif>>
===
title: JackTask1A
tags: 
colorID: 3
position: 982,426
---
//spoken to jack but haven't accepted quest
JACK: I can't do it, Charlie, I can't do it. Please, you have to help me!
->Alright, I'll do it
    <<set $jackaccept to true>> //shortened from "All right I'll give Finn your poem."
    <<set $jackcomplete to false>>
    JACK: Great! Thank you so much, Charlie!
->Maybe later.
//response
===
title: JackCompleted
tags: 
colorID: 2
position: 876,717
---
<<if visited ("JackCompleted") is false>>
    <<if $finnyes is true>>
        //finn accepted and jack is standing with them
        JACK: Charlie! Can you believe it? Finn agreed to go out with me!
        CHARLIE: I know. I was there.
        JACK: THIS IS THE BEST DAY OF MY LIFE!
    <<else>>
        [[Jack3]]
    <<endif>>
<<else>>
    <<if $kimaccept is true and $forest is false>>   //you're looking for spot
        JACK: Hey Charlie.
        CHARLIE: I'm looking for Kim's dog. Have you seen them?
        JACK: I haven't, sorry.
        <<if $finnyes is true>>
            [[JackAndFinn]]
        <<endif>>
    <<else>>    //you haven't agreed to look for spot/you've found spot
        <<if $finnyes is true>>
            [[JackAndFinn]]
        <<else>>
            [[Jack3]]
        <<endif>>
    <<endif>>
<<endif>>
===
title: FinnCompleted
tags: 
colorID: 2
position: 1867,879
---
<<if visited("FinnCompleted") is false>>
    <<if $finnyes is true>> //jack is standing there
        FINN: Hi Charlie. Um, listen...
        FINN: Thanks for saying...what you said. I think it was the right call.
        //point to these options?
        ->You're welcome, Finn.
        ->I hope it works out for you.
        JACK: What are you guys talking about?
        FINN: Oh, nothing.
    <<else>>
        //are these options different enough?
        FINN: How did things go with Jack?  
        ->He took it pretty bad.
            FINN: Poor Jack.
        ->He's fine.
            FINN: That's good, I guess.
        ->He'll get over it.
            FINN: I hope so.
    <<endif>>
<<else>>
    <<if $kimaccept is true and $forest is false>>   //if you've accepted kim's task
        CHARLIE: Have you seen Kim's dog?
        <<if $finnyes is true>>
            [[JackAndFinn]]
        <<else>>
            FINN: No, I haven't.
            FINN: Have you tried asking Dale? They seem to notice every little thing that goes on around here.  
            FINN: They might be able to help.
        <<endif>>
    <<else>>    //if you haven't accepted kim's task/already completed the task
        FINN: Hey Charlie.
    <<endif>>
<<endif>>
===
title: FinnTask2
tags: 
colorID: 3
position: 1766,643
---
//if you've got their response and haven't spoken to jack
FINN: Have you spoken to Jack yet?
CHARLIE: Not yet.
FINN: You should probably do it soon...I don't want to keep them in suspense.


===
title: NOTES
tags: 
colorID: 0
position: 769,-22
---
BLUE: START NODE
GREEN: COMPULSORY NODE
YELLOW: OPTIONAL NODE
ORANGE: OBJECT NODE


//Changing scene:  
//MAKE SURE THIS IS THE LAST THING CALLED (sometimes variables don't set for some reason)
//<<Change Scene [scene no]>> Found by pressing ctrl+shift+b

//Taking objects (calls destroy() on dialogue end)
//Must have the takable script attached
//<<Take [object]>>

===
title: DaleStart
tags: 
colorID: 1
position: 2413,112
---
<<if visited ("RedpeelStart") is false>>
    <<if visited ("DaleGreeting") is false>>    
        [[DaleGreeting]]
    <<else>>
        DALE: You should go and speak to Captain Redpeel at the docks if you want to get to Berry Island.
    <<endif>>
<<else>>
    <<if visited ("KimStart") is false>>
        <<if visited("DaleGreeting") is true>>  
            DALE: Hello again, Charlie. I thought you were going to Berry Island.
            CHARLIE: I don't have enough money.
            DALE: Oh no! 
            DALE: You could try talking to Nancy. They were having some trouble, I believe.
            DALE: And I overhead Jack mention something about needing a messenger...
        <<else>>
            [[DaleGreeting]]
        <<endif>>
    <<else>>
        <<if $kimaccept is true and $forest is false>>
            CHARLIE: Have you seen Kim's dog?
            DALE: I believe I saw it running towards the forest earlier.
        <<else>>
            DALE: Hello there, Charlie. 
        <<endif>>
    <<endif>>
<<endif>>
===
title: KimStart
tags: 
colorID: 1
position: 2674,108
---
<<if visited ("KimStart") is false>>
    KIM: Charlie, you have to help me! I can’t find my dog anywhere! 
    KIM: I’m sure Spot was inside last night but they didn’t come when I called this morning!
    KIM: I’ve searched the whole house and I can’t find them! 
    KIM: What if I left them outside overnight? What if they ran into the forest and got eaten? What if—
    CHARLIE: It’s okay, calm down. I’ll help you look for them.
    KIM: Will you really?
    ->Absolutely.
        KIM: Thank you thank you thank you thank you!
        <<set $kimaccept to true>>
        <<activate Avery>>
    ->For a fee.
        KIM: If you bring my dear Spot home, I'll pay whatever you want.
        <<set $kimaccept to true>>
        <<activate Avery>>
    ->In a bit.
        KIM: Please hurry!
        <<set $kimaccept to false>>
<<else>>
    <<if $kimaccept is true>>   //you've spoken to kim and accepted their task
        <<if $forest is true>> //found dog - avery is with you
            [[Kim2]]
        <<else>> //haven't found dog - haven't gone in to forest
            KIM: Have you found Spot yet?
            CHARLIE: I'm still looking.
            KIM: Please hurry!
        <<endif>>
    <<else>> //you've spoken to kim and haven't accepted their task
        KIM: Please help me find Spot!
        ->I'll go look for them right now.
            KIM: Thank you thank you thank you thank you!
            <<set $kimaccept to true>>
            <<activate Avery>>
        ->I'll look for them soon.
            KIM: Hurry, Charlie! I'm so worried.
    <<endif>>
<<endif>>
===
title: JackAndFinn
tags: 
colorID: 3
position: 1142,915
---
//looking for spot
<<if $kimaccept is true and $forest is false>>
    FINN: Have you tried asking Dale? They seem to notice every little thing that goes on around here.  
    FINN: They might be able to help.
    JACK: That's a great idea, Finn! You're so smart.
<<else>> //not looking for spot/already found spot
    JACK: Hey Charlie! Isn't it a great day!
    FINN: Stop grinning like that, you're going to break your face.
<<endif>>
===
title: Jack3
tags: 
colorID: 3
position: 707,435
---
//if finn rejected jack and charlie hasn't moved on to stage 2/accepted kim's task
JACK: Sorry, Charlie, I don't really feel like talking right now.
<<if visited("Jack3") is false>>
->I'm sorry about Finn.
    JACK: Yeah...me too. I guess...it just wasn't meant to be. 
    JACK: I really thought they were the one, you know?
->You need to toughen up.
    JACK: You can be a real jerk sometimes, Charlie.
    JACK: Just leave me alone.
<<endif>>
===
title: AverySailing
tags: 
colorID: 2
position: 2910,423
---
//after forest and speaking to kim
AVERY: Well, that wasn't too bad. You're a mean hand with that bat.
CHARLIE: Thanks. 
AVERY: I can't stick around - I have to catch Captain Redpeel before they set sail.
CHARLIE: You're going to Berry Island too?
AVERY: Yeah - wait, are you? That's great, we can go together!
AVERY: Come on, we should head to the docks before they leave without us.



===
title: DaleGreeting
tags: 
colorID: 3
position: 2381,401
---
DALE: Hello Charlie! What are you getting up to today?
CHARLIE: I'm going to visit Kell for their birthday.
DALE: Oh, how nice!
DALE: I haven't seen Kell since they moved to Berry Island-what was it, two years ago?
DALE: Please say hello to them for me.
<<if visited("RedpeelStart") is false>>
    DALE: You hurry along to the docks now, you don't want to miss the ship.
<<else>>
    CHARLIE: I need to make some money first. Do you have anything I can do?
    DALE: No, Charlie, I'm sorry.
    DALE: You could try talking to Nancy. They were having some trouble, I believe.
    DALE: And I overhead Jack mention something about wanting a messenger...
<<endif>>
===
title: Nancy2
tags: 
colorID: 2
position: 1489,612
---
NANCY: Thank you for getting rid of those bats, Charlie.
NANCY: Here's a little something for your trouble.
<<set $money to $money + 10>>
NANCY: If you're not busy, I could tell you some more about Harold...
CHARLIE: I'm busy. Very busy.
NANCY: Come back when you're free, dear, and I'll tell you all about them.
<<set $nancycomplete to true>>


<<if $jackcomplete is true and $nancycomplete is true and $houseScene is true>>
    <<activate Kim>>
    <<move Kim Charlie>>
<<endif>>
===
title: Flowers
tags: 
colorID: 4
position: 1317,-214
---
FLOWERS: Pretty.
===
title: Signpost
tags: 
colorID: 4
position: 1689,-224
---
SIGNPOST: A signpost pointing to the docks, the market, and the forest.
===
title: Kim2
tags: 
colorID: 2
position: 2647,420
---
<<if visited ("Kim2") is false>>
//dog appears with kim when you speak to her?
    KIM: Spot! Oh my poor little puppy, where have you been?
    AVERY: We found them in the forest.
    KIM: In the forest? They must have been so scared!
    ->They're safe now.
        KIM: Thank you so much, both of you. 
        KIM: Here, take this as a reward.
    ->Do we get a reward?
        KIM: Oh, uh, yes, of course. Here.
    <<set $money to 55>>
    KIM: Thank you. Now, Spot, you're not going to run away again, are you?
    //SPOT: [barks]
    [[AverySailing]]
<<else>>
    KIM: Thank you so much for finding Spot!
<<endif>>
===
